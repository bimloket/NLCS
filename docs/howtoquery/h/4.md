# Query's

 
## Tabel abibliotheken

de abibliotheken zijn de "arceringsbibliotheken", oftewel de NLCS hoofdgroepen voor arceringen.

De [query voor abibliotheken](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20abibliotheken.rq) resulteert in een tabel zoals deze:

| Abib                                                                           | ID | BIB | Verklaring |
|--------------------------------------------------------------------------------|----|-----|------------|
| https://www.example.org/nlcs/def/02ffcdec-87e9-4ea3-8aa6-7ae6d32470c4 |    | ACO |            |
| https://www.example.org/nlcs/def/95d72fe5-9222-417d-a5d6-dcc9b296a9e4 |    | AGR |            |
| https://www.example.org/nlcs/def/00741035-a28a-4e9d-89e1-c581c7fdb8a5 |    | AGW |            |
| https://www.example.org/nlcs/def/afa07f71-45f2-4916-b210-50beaab8dae8 |    | AIE |            |
| https://www.example.org/nlcs/def/3e69acee-1f48-4639-b5af-0733112f36c9 |    | AKG |            |
| https://www.example.org/nlcs/def/e8815404-b4dd-40bf-84fe-d26f19ba42a5 |    | AMO |            |
| https://www.example.org/nlcs/def/d9369bd0-3741-4a9f-b35c-c5feeb497199 |    | AMW |            |
| https://www.example.org/nlcs/def/b8587641-c900-4217-9a69-f0a3cdb41656 |    | AOG |            |
| https://www.example.org/nlcs/def/ea441d5e-933e-4662-92b3-c8526feb5ded |    | AVH |            |



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

SELECT DISTINCT ?Abib  ?ID ?BIB ?Verklaring
WHERE {
  ?NLCSObjectTopConcept a owl:Class ;
                        skos:prefLabel "Arcering" .
  ?Abib rdfs:subClassOf ?NLCSObjectTopConcept ;
        skos:prefLabel ?BIB .
  OPTIONAL {
    ?Abib skos:definition ?Verklaring .
  }
  # Deze OPTIONAL kan mogelijk niet correct functioneren zonder meer specifieke details over de structuur en beschikbaarheid van ID eigenschappen binnen je data
  OPTIONAL {
    ?IDProperty skos:prefLabel "ID" .
    ?Abib ?IDProperty ?ID .
  }
}
ORDER BY ?BIB
</pre>




## Tabel arceringen

De [query voor arceringen](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20arceringen.rq) resulteert in een tabel zoals deze, gespiegeld voor betere leesbaarheid:

|                           | Row 1                                                  | Row 2                                                  | Row 3                                                  | Row 4                                                  |
|---------------------------|--------------------------------------------------------|--------------------------------------------------------|--------------------------------------------------------|--------------------------------------------------------|
| **FASE**                  |                                                        |                                                        |                                                        |                                                        |
| **BIB**                   | ACO                                                    | ACO                                                    | ACO                                                    | ACO                                                    |
| **ARCERING**              | ACO-HOUT_02-SODMM                                      | ACO-HOUT_03-SODMM                                      | ACO-HOUT_01-SODMM                                      | ACO-BETON_VOORGESP_PREFAB-SODMM                        |
| **opt**                   | SODMM                                                  | SODMM                                                  | SODMM                                                  | SODMM                                                  |
| **Arcering**              | https://www.example.org/nlcs/def/55c1f87b-535c-4532-ace8-9e62694488db | hhttps://www.example.org/nlcs/def/37d2c991-aebd-4df0-8c12-8361d4df6cd8 | hhttps://www.example.org/nlcs/def/338b9a47-db06-4856-91df-01ef09f3d7e5 | hhttps://www.example.org/nlcs/def/37868743-baf2-4572-a427-9f460de5725a |
| **ID**                    | 19                                                     | 20                                                     | 18                                                     | 12                                                     |
| **VRKL_KORT**             | hout02                                                 | hout03                                                 | hout01                                                 | prefabbeton                                            |
| **VRKL_LANG**             | 45,0,0,0,0.2                                           | 0,0,0,0,0.3                                            | 45,0,0,0,1.875 135,0,0,3.75,1.875,4.6875,-2.8125       | 45,0,0,0,2.5 -45,0,0,0,2.5 0,0,0,0,.75,0,-0.75        |


De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

SELECT DISTINCT ?FASE ?BIB ?ARCERING ?opt ?Arcering ?ID ?VRKL_KORT ?VRKL_LANG 
WHERE {
  ?ArceringTopConcept a owl:Class .
  ?ArceringTopConcept skos:prefLabel "Arcering" .
  ?Abib rdfs:subClassOf ?ArceringTopConcept .
  ?Abib skos:prefLabel ?BIB .
  ?Arcering rdfs:subClassOf+ ?Abib .
  ?Arcering skos:prefLabel ?ARCERING .
  FILTER (CONTAINS(STR(?ARCERING), STR(CONCAT(STR(?BIB), "-")))) 
  OPTIONAL {
    BIND (STRBEFORE(STR(?ARCERING), STR(CONCAT(CONCAT("-", STR(?BIB)), "-" ))) AS ?FASE) .
    OPTIONAL { BIND ( STRAFTER(SUBSTR(?ARCERING,5), "-") AS ?opt) }
  } 
  OPTIONAL {
    ?Arcering ?IDProperty ?ID .
    ?IDProperty skos:prefLabel "ID" .
  } 
  OPTIONAL {
    ?Arcering ?VRKL_KORTProperty ?VRKL_KORT .
    ?VRKL_KORTProperty skos:prefLabel "VRKL_KORT" .
  } 
  OPTIONAL {
    ?Arcering ?VRKL_LANGProperty ?VRKL_LANG .
    ?VRKL_LANGProperty skos:prefLabel "VRKL_LANG" .
  } 
}
</pre>

## Tabel disciplines
De [query voor disciplines](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20disciplines.rq) resulteert in een tabel zoals deze:

| DISCIPLINE | Code | Verklaring |
|------------|------|-------------|
| http://www.example.org/nlcs/def/4adb2e90-c529-450e-ad37-fe254e218e90 | CO | Vorm- en wapeningstekeningen van civieltechnische constructies, zoals: brug, tunnel, onderdoorgang, viaduct, sluizencomplex, spoortunnel, faunapassage. |
| http://www.example.org/nlcs/def/90958d82-e5b2-4103-8c16-b7c6e297638d | GV | Vormgeving van landschappelijke en stedelijk groene inrichting van de omgeving ten behoeve van beplantingsplannen, groenbeheerplannen, groenstructuurplannen en dergelijke. Situatie-, dwars- en lengteprofielen en detailtekeningen. |
| http://www.example.org/nlcs/def/c6bc6ff1-1327-4054-b3a0-5b2b75ef7b50 | ME | Landmeetkundige tekeningen Bijvoorbeeld: het genereren van een GIS-model op basis van het IMgeo protocol vanuit een NLCS model. |
| http://www.example.org/nlcs/def/91118d23-c7fd-44e9-a7d0-5a77f74184c4 | MI | Vormgeving van (water)bodemsaneringen, inclusief situatie-, dwars- en lengteprofielen en detailtekeningen. |
| http://www.example.org/nlcs/def/c25b88c7-ab0a-48c4-b0c3-589232d24ddd | OI | Vormgeving van de ondergrondse infrastructuur (kabels en leidingen). Bestaande situatie inclusief te verwijderen en toekomstige situatie. Situatie-, dwars- en lengteprofielen en detailtekeningen. |




De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Discipline information including their URI, and stores it in the familiar Excel/CSV format

SELECT DISTINCT ?DISCIPLINE ?Code ?Verklaring
WHERE {
    ?DisciplineTopConcept skos:prefLabel "Disciplines" .
    ?DISCIPLINE a ?DisciplineTopConcept ;
                skos:prefLabel ?Code ;
                skos:definition ?Verklaring .
} ORDER BY ?Code
</pre>

<div class="issue" data-number="262"></div>


## Tabellen hoofdgroep
De [query voor de tabellen van de hoofdgroepen](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20hoofdgroepen.rq) resulteert in een tabel zoals deze, de rijen en kolommen zijn omgekeerd ten behoeve van de leesbaarheid in dit document:

| NLCSObject | http://www.example.org/nlcs/def/c132cd12-d85e-46d8-b862-27c6d713a883 | http://www.example.org/nlcs/def/e1303c6d-5701-4ffb-a68f-1f9b42aa049c | http://www.example.org/nlcs/def/1e16b5eb-015c-4c1f-9d10-6ba71dfa167b |
|------------|------------------------------------------------------------------------|------------------------------------------------------------------------|------------------------------------------------------------------------|
| OMSCHRIJVING | HOUTVORM | AANGRENZENDE CONSTRUCTIE | VERBINDING |
| STATUS | * | * | * |
| DISCIPLINE | ** | ** | ** |
| Hoofdgroep | HC | HC | HC |
| Object | HOUTVORM | AANGRENZENDE CONSTRUCTIE | VERBINDING |
| SubObject01 |  |  |  |
| SubObject02 |  |  |  |
| SubObject03 |  |  |  |
| SubObject04 |  |  |  |
| SubObject05 |  |  |  |
| Bewerking |  |  |  |
| Element | G/A | G | G |
| Arcering |  |  |  |
| Symbool |  |  |  |
| Laagnaam | *-**-HC-HOUTVORM-G/A | *-**-HC-AANGRENZENDE CONSTRUCTIE-G | *-**-HC-VERBINDING-G |
| BLineweight | 0.18 | 0.18 | 0.18 |
| BColor | 30 | 7 | 140 |
| BColorA | 252 | 253 | 252 |
| BColorGD | 40 | 7 | 130 |
| BColorGN | 10 | 20 | 10 |
| BColorV | 253 | 253 | 253 |
| B_Linetype | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| NLineweight | 0.25 | 0.18 | 0.18 |
| NColor | 30 | 7 | 140 |
| NColorA | 252 | 253 | 252 |
| NColorGD | 40 | 7 | 130 |
| NColorGN | 10 | 20 | 10 |
| NColorV | 253 | 253 | 253 |
| N_Linetype | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| VLineweight | 0.25 | 0.18 | 0.18 |
| VColor | 30 | 7 | 140 |
| VColorA | 252 | 253 | 252 |
| VColorGD | 40 | 7 | 130 |
| VColorGN | 10 | 20 | 10 |
| VColorV | 253 | 253 | 253 |
| V_Linetype | V-CONTINUOUS-SO | V-CONTINUOUS-SO | V-CONTINUOUS-SO |
| TLineweight | 0.25 | 0.18 | 0.18 |
| TColor | 30 | 7 | 140 |
| TColorA | 252 | 253 | 252 |
| TColorGD | 40 | 7 | 130 |
| TColorGN | 10 | 20 | 10 |
| TColorV | 253 | 253 | 253 |
| T_Linetype | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| VRKL_KORT |  |  |  |
| VRKL_LANG |  |  |  |
| ID | 2797 | 2803 | 2800 |
| KIND_VAN |  |  |  |



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Hoofdgroep information and stores it in the familiar Excel/CSV format

SELECT distinct 
?NLCSObject  ?OMSCHRIJVING ?STATUS ?DISCIPLINE ?Hoofdgroep ?Object ?SubObject01 ?SubObject02 ?SubObject03 ?SubObject04 ?SubObject05 ?Bewerking ?Element ?Arcering ?Symbool ?Laagnaam 
?BLineweight ?BColor ?BColorA ?BColorGD ?BColorGN ?BColorV ?B_Linetype 
?NLineweight ?NColor ?NColorA ?NColorGD ?NColorGN ?NColorV ?N_Linetype 
?VLineweight ?VColor ?VColorA ?VColorGD ?VColorGN ?VColorV ?V_Linetype 
?TLineweight ?TColor ?TColorA ?TColorGD ?TColorGN ?TColorV ?T_Linetype
?VRKL_KORT ?VRKL_LANG ?ID ?KIND_VAN
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  ?Hoofdgroep2 rdfs:subClassOf ?NLCSObjectTopConcept .
  ?Hoofdgroep2 skos:prefLabel ?HOOFDGROEP .
  ?NLCSObject rdfs:subClassOf+ ?Hoofdgroep2 .
  ?NLCSObject skos:prefLabel ?OMSCHRIJVING .
  
  ?NLCSObject rdfs:subClassOf ?bnodeStatus .
  ?bnodeStatus owl:onProperty ?StatusProperty .
  ?StatusProperty skos:prefLabel "Status" .
  OPTIONAL {
    ?StatusProperty owl:hasValue/skos:prefLabel ?StatusValue .
  }
  BIND( COALESCE(?StatusValue, "*") AS ?STATUS) .
  ?NLCSObject rdfs:subClassOf ?bnodeDiscipline .
  ?bnodeDiscipline owl:onProperty ?DisciplineProperty .
  ?DisciplineProperty skos:prefLabel "Discipline" .
  OPTIONAL {
    ?DisciplineProperty owl:hasValue/skos:prefLabel ?DisciplineValue .
  }
  BIND( COALESCE(?DisciplineValue, "**") AS ?DISCIPLINE) .
  OPTIONAL {
    ?NLCSObject rdfs:subClassOf ?bnodeArcering .
    ?bnodeArcering owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
    ?bnodeArcering owl:onClass ?ARCERING .
    ?ARCERING rdfs:subClassOf* ?ArceringTopConcept .
    ?ArceringTopConcept skos:prefLabel "Arcering" .
    ?ARCERING skos:altLabel ?Arcering .
    FILTER( CONTAINS(STR(?ARCERING), "https://www.example.org/nlcs/def/"))
  } OPTIONAL {
    ?NLCSObject rdfs:subClassOf ?bnodeSymbool .
    ?bnodeSymbool owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
    ?bnodeSymbool owl:onClass ?SYMBOOL .
    ?SYMBOOL rdfs:subClassOf* ?SymboolTopConcept .
    ?SymboolTopConcept skos:prefLabel "Symbool" .
    ?SYMBOOL skos:altLabel ?Symbool .
    FILTER( CONTAINS(STR(?SYMBOOL), "https://www.example.org/nlcs/def/"))
  }
  ?NLCSObject ?LaagnaamProperty ?Laagnaam .
  ?LaagnaamProperty skos:prefLabel "Laagnaam" .
  OPTIONAL {
    ?NLCSObject ?BLineweightProperty ?BLineweight .
    ?BLineweightProperty skos:prefLabel "B lineweight" .
    ?NLCSObject ?BColorProperty ?BColor .
    ?BColorProperty skos:prefLabel "B color" .
    ?NLCSObject ?BColorAProperty ?BColorA .
    ?BColorAProperty skos:prefLabel "B color A" .
    ?NLCSObject ?BColorGDProperty ?BColorGD .
    ?BColorGDProperty skos:prefLabel "B color GD" .
    ?NLCSObject ?BColorGNProperty ?BColorGN .
    ?BColorGNProperty skos:prefLabel "B color GN" .
    ?NLCSObject ?BColorVProperty ?BColorV .
    ?BColorVProperty skos:prefLabel "B color V" .
    ?NLCSObject ?BLinetypeProperty ?BLinetype .
    ?BLinetypeProperty skos:prefLabel "B linetype" .
    ?BLinetype skos:prefLabel ?B_Linetype .
  } OPTIONAL {
    ?NLCSObject ?NLineweightProperty ?NLineweight .
    ?NLineweightProperty skos:prefLabel "N lineweight" .
    ?NLCSObject ?NColorProperty ?NColor .
    ?NColorProperty skos:prefLabel "N color" .
    ?NLCSObject ?NColorAProperty ?NColorA .
    ?NColorAProperty skos:prefLabel "N color A" .
    ?NLCSObject ?NColorGDProperty ?NColorGD .
    ?NColorGDProperty skos:prefLabel "N color GD" .
    ?NLCSObject ?NColorGNProperty ?NColorGN .
    ?NColorGNProperty skos:prefLabel "N color GN" .
    ?NLCSObject ?NColorVProperty ?NColorV .
    ?NColorVProperty skos:prefLabel "N color V" .
    ?NLCSObject ?NLinetypeProperty ?NLinetype .
    ?NLinetypeProperty skos:prefLabel "N linetype" .
    ?NLinetype skos:prefLabel ?N_Linetype .
  } OPTIONAL {
    ?NLCSObject ?TLineweightProperty ?TLineweight .
    ?TLineweightProperty skos:prefLabel "T lineweight" .
    ?NLCSObject ?TColorProperty ?TColor .
    ?TColorProperty skos:prefLabel "T color" .
    ?NLCSObject ?TColorAProperty ?TColorA .
    ?TColorAProperty skos:prefLabel "T color A" .
    ?NLCSObject ?TColorGDProperty ?TColorGD .
    ?TColorGDProperty skos:prefLabel "T color GD" .
    ?NLCSObject ?TColorGNProperty ?TColorGN .
    ?TColorGNProperty skos:prefLabel "T color GN" .
    ?NLCSObject ?TColorVProperty ?TColorV .
    ?TColorVProperty skos:prefLabel "T color V" .
    ?NLCSObject ?TLinetypeProperty ?TLinetype .
    ?TLinetypeProperty skos:prefLabel "T linetype" .
    ?TLinetype skos:prefLabel ?T_Linetype .
  } OPTIONAL {
    ?NLCSObject ?VLineweightProperty ?VLineweight .
    ?VLineweightProperty skos:prefLabel "V lineweight" .
    ?NLCSObject ?VColorProperty ?VColor .
    ?VColorProperty skos:prefLabel "V color" .
    ?NLCSObject ?VColorAProperty ?VColorA .
    ?VColorAProperty skos:prefLabel "V color A" .
    ?NLCSObject ?VColorGDProperty ?VColorGD .
    ?VColorGDProperty skos:prefLabel "V color GD" .
    ?NLCSObject ?VColorGNProperty ?VColorGN .
    ?VColorGNProperty skos:prefLabel "V color GN" .
    ?NLCSObject ?VColorVProperty ?VColorV .
    ?VColorVProperty skos:prefLabel "V color V" .
    ?NLCSObject ?VLinetypeProperty ?VLinetype .
    ?VLinetypeProperty skos:prefLabel "V linetype" .
    ?VLinetype skos:prefLabel ?V_Linetype .
  }
  OPTIONAL {
    ?NLCSObject ?VRKLProperty ?VRKL_KORT .
    ?VRKLProperty skos:prefLabel "VRKL_KORT" .
  } OPTIONAL {
    ?NLCSObject ?IDProperty ?ID .
    ?IDProperty skos:prefLabel "ID" .
  } OPTIONAL {
    ?NLCSObject rdfs:subClassOf ?NLCSParentObject .
    ?NLCSParentObject ?IDPropertyParent ?KIND_VAN .
    ?IDPropertyParent skos:prefLabel "ID" .
  } 
    OPTIONAL {
    ?NLCSObject ?VRKL_LANGProperty ?VRKL_LANG .
    ?VRKL_LANGProperty skos:prefLabel "VRKL_LANG" .
  } 
    bind (strbefore (?Laagnaam,"-") as ?Status)
    bind (strbefore (strafter (?Laagnaam,"-"),"-") as ?Discipline)
	  bind (strbefore (strafter (strafter (?Laagnaam,"-"),"-"),"-") as ?Hoofdgroep)
    bind (strafter(strafter (strafter (?Laagnaam,"-"),"-"),"-") as ?ObjectTotal )
    bind (strafter (strafter (strafter (?Laagnaam,"-"),"-"),"-") as ?ElementPrePre )
    bind ( if ( contains(?ElementPrePre, "-"), strafter(?ElementPrePre, "-"),?ElementPrePre) as ?ElementPre )
    bind ( if ( contains(?ElementPre, "-"), strafter(?ElementPre, "-"),?ElementPre) as ?Element )
    bind ( if ( contains(?ObjectTotal, "_"), strbefore(?ObjectTotal, "_"),strbefore(?ObjectTotal, "-")) as ?Object )
    bind ( if ( contains(?ObjectTotal, "_"), strafter(?ObjectTotal, "_"),strafter(?ObjectTotal, "-")) as ?SubObject01Total )
    bind ( if ( contains(?SubObject01Total, "_"), strbefore(?SubObject01Total, "_"),strbefore(?SubObject01Total, "-")) as ?SubObject01 )
    bind ( if ( contains(?SubObject01Total, "_"), strafter(?SubObject01Total, "_"),strafter(?SubObject01Total, "-")) as ?SubObject02Total )
    bind ( if ( contains(?SubObject02Total, "_"), strbefore(?SubObject02Total, "_"),strbefore(?SubObject02Total, "-")) as ?SubObject02 )
    bind ( if ( contains(?SubObject02Total, "_"), strafter(?SubObject02Total, "_"),strafter(?SubObject02Total, "-")) as ?SubObject03Total )
    bind ( if ( contains(?SubObject03Total, "_"), strbefore(?SubObject03Total, "_"),strbefore(?SubObject03Total, "-")) as ?SubObject03 )
    bind ( if ( contains(?SubObject03Total, "_"), strafter(?SubObject03Total, "_"),strafter(?SubObject03Total, "-")) as ?SubObject04Total )
    bind ( if ( contains(?SubObject04Total, "_"), strbefore(?SubObject04Total, "_"),strbefore(?SubObject04Total, "-")) as ?SubObject04 )
    bind ( if ( contains(?SubObject04Total, "_"), strafter(?SubObject04Total, "_"),strafter(?SubObject04Total, "-")) as ?SubObject05Total )
    bind ( if ( contains(?SubObject05Total, "_"), strbefore(?SubObject05Total, "_"),strbefore(?SubObject05Total, "-")) as ?SubObject05 )
    bind ( if ( contains(?SubObject05Total, "_"), strafter(?SubObject05Total, "_"),strafter(?SubObject05Total, "-")) as ?BewerkingTotal )
    bind ( if ( contains(?BewerkingTotal, "_"), strbefore(?BewerkingTotal, "_"),strbefore(?BewerkingTotal, "-")) as ?Bewerking )
}
</pre>

<div class="issue" data-number="263"></div>





## Hoofdgroep afkortingen
De [query voor afkortingen van hoofdgroepen](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20hoofdgroepen%20afkortingen.rq) resulteert in een tabel zoals deze:



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all general Hoofdgroep information including the URI and stores it in the familiar Excel/CSV format

SELECT DISTINCT ?Afkorting ?HoofdgroepURI ?Hoofdgroep ?ID
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  ?HoofdgroepURI rdfs:subClassOf ?NLCSObjectTopConcept .
  ?HoofdgroepURI skos:prefLabel ?Afkorting ;
               skos:altLabel ?Hoofdgroep .
  OPTIONAL {
    ?HoofdgroepURI ?IDProperty ?ID .
    ?IDProperty skos:prefLabel "ID" .
  }
} ORDER BY ?Afkorting
</pre>





## Tabel lijnkleuren
De [query voor lijnkleuren]() resulteert in een tabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
</pre>

<div class="issue" data-number="269"></div>


## Tabel lijntypes
De [query voor lijntypes](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20lijntypes.rq) resulteert in een tabel zoals deze:

| ID  | HOOFDGROEP | LIJNTYPE          | DEFINITIE                                            | opt | FASE | STATUS |
| --- | ---------- | ----------------- | ---------------------------------------------------- | --- | ---- | ------ |
| 10  | AM         | AM-AS-SO          | A,10,-2,2,-2, SO                                     |     |      | *      |
| 106 | KG         | KG-LAND-SO         | A,0,[KRUISJE,NLCS.SHX,s=1],-3.5, SO                  |     |      | *      |
| 107 | KG         | KG-PROVINCIE-SO    | A,2.5,-2.75,[KRUISJE,NLCS.SHX,s=1],-2.75, SO        |     |      | *      |
| 108 | KG         | KG-GEMEENTE-SO     | A,1,-2.25,[KRUISJE,NLCS.SHX,s=1],-2.25, SO          |     |      | *      |
| 109 | KG         | KG-WATERSCHAP-SO   | A,5.5,-2,[KRUISJE,NLCS.SHX,s=0.75],-3,[KRUISJE,NLCS.SHX,s=0.75],-2, SO |     |      | *      |
| 11  | AM         | V-AM-AS-SO         | A,10,[VERWIJDEREN2,NLCS.SHX,x=-5,s=1],-2,2,-2, SO | V   |      | *      |


De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Lijntype information and stores it in the familiar Excel/CSV format

SELECT DISTINCT ?ID ?HOOFDGROEP ?LIJNTYPE ?DEFINITIE ?opt ?FASE ?STATUS
WHERE {
  ?LijntypeTopConcept a owl:Class .
  ?LijntypeTopConcept skos:prefLabel "Lijntype" .
  ?Hoofdgroep rdfs:subClassOf ?LijntypeTopConcept .
  ?Hoofdgroep skos:prefLabel ?HOOFDGROEP .
  ?Lijntype rdfs:subClassOf+ ?Hoofdgroep .
  ?Lijntype skos:prefLabel ?LIJNTYPE .
  BIND ( IF(?Lijntype = ?Hoofdgroep, IF(?LijntypeName IN ("V-CONTINUOUS-SO", "CONTINUOUS"), ?Lijntype, ?undefined), ?Lijntype) AS ?NotHoofdgroep) .
  FILTER (?Lijntype = ?NotHoofdgroep) .
    optional { bind ( strafter(?LIJNTYPE,"-") as ?opt2)
  bind ( if ( contains(?opt2,"-"),strafter(?opt2,"-"),?opt2) as ?opt3 )
  bind ( if ( contains(?opt3,"-"),strafter(?opt3,"-"),?opt3) as ?opt4 )
        bind ( if (strlen(?opt4)>4,"",?opt4) as ?opt ) }
  OPTIONAL {
    ?Lijntype ?IDProperty ?ID .
    ?IDProperty skos:prefLabel "ID" .
  } OPTIONAL {
    ?Lijntype skos:definition ?DEFINITIE .
  } 
      OPTIONAL {
    BIND (STRBEFORE(STR(?LIJNTYPE), STR(CONCAT(CONCAT("-", STR(?HOOFDGROEP)), "-" ))) AS ?FASE) . }
    OPTIONAL {
    ?Lijntype rdfs:subClassOf ?bnodeStatus .
  ?bnodeStatus owl:onProperty ?StatusProperty .
  ?StatusProperty skos:prefLabel "Status" .
        optional {
    ?StatusProperty owl:hasValue/skos:prefLabel ?StatusValue .
    }
  }
  BIND( COALESCE(?StatusValue, "*") AS ?STATUS) .
}
orderby ?ID
</pre>


## Tabel lijnweights
De [query voor lijndiktes](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20lineweights.rq) resulteert in een tabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
select distinct ?Lineweight
where { 
	   {
    ?NLCSObject ?BLineweightProperty ?Lineweight .
    ?BLineweightProperty skos:prefLabel "B lineweight" .
  } 
    union {
    ?NLCSObject ?NLineweightProperty ?Lineweight .
    ?NLineweightProperty skos:prefLabel "N lineweight" .
  } 
    union {
    ?NLCSObject ?TLineweightProperty ?Lineweight .
    ?TLineweightProperty skos:prefLabel "T lineweight" .
  } 
    union {
    ?NLCSObject ?VLineweightProperty ?Lineweight .
    ?VLineweightProperty skos:prefLabel "V lineweight" .
  }
} 
orderby ?Lineweight
</pre>

<div class="issue" data-number="273"></div>

## Tabel object
De [query voor een object](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20NLCS-objects.rq) resulteert in een tabel zoals deze, gespiegeld voor leesbaarheid:

De toelichting op de onderdelen van deze tabel staan in de [Code-documentatie](https://nl-digigo.github.io/NLCS/code_documentation/#objecten)

| NLCSObject                                      | https://www.example.org/nlcs/def/c132cd12-d85e-46d8-b862-27c6d713a883 | https://www.example.org/nlcs/def/e1303c6d-5701-4ffb-a68f-1f9b42aa049c | https://www.example.org/nlcs/def/1e16b5eb-015c-4c1f-9d10-6ba71dfa167b |
| ------------------------------------------------ | -------------------------------------------------------------------- | -------------------------------------------------------------------- | -------------------------------------------------------------------- |
| OMSCHRIJVING                   | HOUTVORM                      | AANGRENZENDE CONSTRUCTIE       | VERBINDING                    |
| STATUS | *      | *      | *      |
| DISCIPLINE | **         | **         | **         |
| HOOFDGROEP | HC         | HC         | HC         |
| ARCERING |          |          |          |
| ARCERINGName | *-**-HC-HOUTVORM-G/A | *-**-HC-AANGRENZENDE CONSTRUCTIE-G | *-**-HC-VERBINDING-G |
| SYMBOOL | 0.18    | 0.18    | 0.18    |
| SYMBOOLName | 30           | 7            | 140          |
| LAAGNAAM | 252      | 253      | 252      |
| BLineweight | 40           | 7            | 130          |
| BColor | 10     | 20     | 10     |
| BColorA | 253     | 253     | 253     |
| BColorGD | 40       | 7        | 130      |
| BColorGN | 10      | 20      | 10      |
| BColorV | 253     | 253     | 253     |
| BLinetype | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| BLinetypeName | 0.25         | 0.18         | 0.18         |
| NLineweight | 30     | 7      | 140    |
| NColor | 252     | 253    | 252    |
| NColorA | 40       | 7        | 130      |
| NColorGD | 10      | 20      | 10      |
| NColorGN | 253     | 253     | 253     |
| NColorV | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| NLinetype | 0.25         | 0.18         | 0.18         |
| NLinetypeName | 30     | 7      | 140    |
| TLineweight | 252     | 253    | 252    |
| TColor | 40       | 10      | 130      |
| TColorA | 10      | 20      | 10      |
| TColorGD | 253     | 253     | 253     |
| TColorGN | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| TColorV | 0.25         | 0.18         | 0.18         |
| TLinetype | 30     | 7      | 140    |
| TLinetypeName | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| VLineweight | 0.25         | 0.18         | 0.18         |
| VColor | 30     | 7      | 140    |
| VColorA | 252     | 253    | 252    |
| VColorGD | 40       | 7        | 130      |
| VColorGN | 10      | 20      | 10      |
| VColorV | 253     | 253     | 253     |
| VLinetype | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| VLinetypeName | 0.25         | 0.18         | 0.18         |
| VRKL_KORT | G/A | G | G |
| VRKL_LANG | HOUT | AANGRENZENDE CONSTRUCTIE | VERBINDING |
| ID | 2797      | 2803      | 2800      |
| KIND_VAN |           |           |           |
| FASE |           |           |           |
| Bewerking |           |           |           |
| Element |           |           |           |



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all NLCS-Object information and stores it in the familiar Excel/CSV format

SELECT ?NLCSObject ?OMSCHRIJVING ?STATUS ?DISCIPLINE ?HOOFDGROEP ?ARCERING ?ARCERINGName ?SYMBOOL ?SYMBOOLName ?LAAGNAAM 
?BLineweight ?BColor ?BColorA ?BColorGD ?BColorGN ?BColorV ?BLinetype ?BLinetypeName
?NLineweight ?NColor ?NColorA ?NColorGD ?NColorGN ?NColorV ?NLinetype ?NLinetypeName
?TLineweight ?TColor ?TColorA ?TColorGD ?TColorGN ?TColorV ?TLinetype ?TLinetypeName
?VLineweight ?VColor ?VColorA ?VColorGD ?VColorGN ?VColorV ?VLinetype ?VLinetypeName
?VRKL_KORT ?VRKL_LANG ?ID ?KIND_VAN ?FASE ?Bewerking ?Element
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  ?Hoofdgroep rdfs:subClassOf ?NLCSObjectTopConcept .
  ?Hoofdgroep skos:prefLabel ?HOOFDGROEP .
  ?NLCSObject rdfs:subClassOf+ ?Hoofdgroep .
  ?NLCSObject skos:prefLabel ?OMSCHRIJVING .
  
  ?NLCSObject rdfs:subClassOf ?bnodeStatus .
  ?bnodeStatus owl:onProperty ?StatusProperty .
  ?StatusProperty skos:prefLabel "Status" .
  OPTIONAL {
    ?StatusProperty owl:hasValue/skos:prefLabel ?StatusValue .
  }
  BIND( COALESCE(?StatusValue, "*") AS ?STATUS) .
  ?NLCSObject rdfs:subClassOf ?bnodeDiscipline .
  ?bnodeDiscipline owl:onProperty ?DisciplineProperty .
  ?DisciplineProperty skos:prefLabel "Discipline" .
  OPTIONAL {
    ?DisciplineProperty owl:hasValue/skos:prefLabel ?DisciplineValue .
  }
  BIND( COALESCE(?DisciplineValue, "**") AS ?DISCIPLINE) .
  OPTIONAL {
    ?NLCSObject rdfs:subClassOf ?bnodeArcering .
    ?bnodeArcering owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
    ?bnodeArcering owl:onClass ?ARCERING .
    ?ARCERING rdfs:subClassOf* ?ArceringTopConcept .
    ?ArceringTopConcept skos:prefLabel "Arcering" .
    ?ARCERING skos:prefLabel ?ARCERINGName .
    FILTER( CONTAINS(STR(?ARCERING), "https://www.example.org/nlcs/def/"))
  } OPTIONAL {
    ?NLCSObject rdfs:subClassOf ?bnodeSymbool .
    ?bnodeSymbool owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
    ?bnodeSymbool owl:onClass ?SYMBOOL .
    ?SYMBOOL rdfs:subClassOf* ?SymboolTopConcept .
    ?SymboolTopConcept skos:prefLabel "Symbool" .
    ?SYMBOOL skos:prefLabel ?SYMBOOLName .
    FILTER( CONTAINS(STR(?SYMBOOL), "https://www.example.org/nlcs/def/"))
  }
    OPTIONAL {    BIND (STRBEFORE(STR(?SYMBOOL), "-") AS ?FASE ) . }
    ?NLCSObject ?LaagnaamProperty ?LAAGNAAM .
    ?LaagnaamProperty skos:prefLabel "Laagnaam" .
  OPTIONAL {
    ?NLCSObject ?BLineweightProperty ?BLineweight .
    ?BLineweightProperty skos:prefLabel "B lineweight" .
    ?NLCSObject ?BColorProperty ?BColor .
    ?BColorProperty skos:prefLabel "B color" .
    ?NLCSObject ?BColorAProperty ?BColorA .
    ?BColorAProperty skos:prefLabel "B color A" .
    ?NLCSObject ?BColorGDProperty ?BColorGD .
    ?BColorGDProperty skos:prefLabel "B color GD" .
    ?NLCSObject ?BColorGNProperty ?BColorGN .
    ?BColorGNProperty skos:prefLabel "B color GN" .
    ?NLCSObject ?BColorVProperty ?BColorV .
    ?BColorVProperty skos:prefLabel "B color V" .
    ?NLCSObject ?BLinetypeProperty ?BLinetype .
    ?BLinetypeProperty skos:prefLabel "B linetype" .
    ?BLinetype skos:prefLabel ?BLinetypeName .
  } OPTIONAL {
    ?NLCSObject ?NLineweightProperty ?NLineweight .
    ?NLineweightProperty skos:prefLabel "N lineweight" .
    ?NLCSObject ?NColorProperty ?NColor .
    ?NColorProperty skos:prefLabel "N color" .
    ?NLCSObject ?NColorAProperty ?NColorA .
    ?NColorAProperty skos:prefLabel "N color A" .
    ?NLCSObject ?NColorGDProperty ?NColorGD .
    ?NColorGDProperty skos:prefLabel "N color GD" .
    ?NLCSObject ?NColorGNProperty ?NColorGN .
    ?NColorGNProperty skos:prefLabel "N color GN" .
    ?NLCSObject ?NColorVProperty ?NColorV .
    ?NColorVProperty skos:prefLabel "N color V" .
    ?NLCSObject ?NLinetypeProperty ?NLinetype .
    ?NLinetypeProperty skos:prefLabel "N linetype" .
    ?NLinetype skos:prefLabel ?NLinetypeName .
  } OPTIONAL {
    ?NLCSObject ?TLineweightProperty ?TLineweight .
    ?TLineweightProperty skos:prefLabel "T lineweight" .
    ?NLCSObject ?TColorProperty ?TColor .
    ?TColorProperty skos:prefLabel "T color" .
    ?NLCSObject ?TColorAProperty ?TColorA .
    ?TColorAProperty skos:prefLabel "T color A" .
    ?NLCSObject ?TColorGDProperty ?TColorGD .
    ?TColorGDProperty skos:prefLabel "T color GD" .
    ?NLCSObject ?TColorGNProperty ?TColorGN .
    ?TColorGNProperty skos:prefLabel "T color GN" .
    ?NLCSObject ?TColorVProperty ?TColorV .
    ?TColorVProperty skos:prefLabel "T color V" .
    ?NLCSObject ?TLinetypeProperty ?TLinetype .
    ?TLinetypeProperty skos:prefLabel "T linetype" .
    ?TLinetype skos:prefLabel ?TLinetypeName .
  } OPTIONAL {
    ?NLCSObject ?VLineweightProperty ?VLineweight .
    ?VLineweightProperty skos:prefLabel "V lineweight" .
    ?NLCSObject ?VColorProperty ?VColor .
    ?VColorProperty skos:prefLabel "V color" .
    ?NLCSObject ?VColorAProperty ?VColorA .
    ?VColorAProperty skos:prefLabel "V color A" .
    ?NLCSObject ?VColorGDProperty ?VColorGD .
    ?VColorGDProperty skos:prefLabel "V color GD" .
    ?NLCSObject ?VColorGNProperty ?VColorGN .
    ?VColorGNProperty skos:prefLabel "V color GN" .
    ?NLCSObject ?VColorVProperty ?VColorV .
    ?VColorVProperty skos:prefLabel "V color V" .
    ?NLCSObject ?VLinetypeProperty ?VLinetype .
    ?VLinetypeProperty skos:prefLabel "V linetype" .
    ?VLinetype skos:prefLabel ?VLinetypeName .
  }
  OPTIONAL {
    ?NLCSObject ?VRKLProperty ?VRKL_KORT .
    ?VRKLProperty skos:prefLabel "VRKL_KORT" .
  } OPTIONAL {
    ?NLCSObject ?IDProperty ?ID .
    ?IDProperty skos:prefLabel "ID" .
  } OPTIONAL {
    ?NLCSObject rdfs:subClassOf ?NLCSParentObject .
    ?NLCSParentObject ?IDPropertyParent ?KIND_VAN .
    ?IDPropertyParent skos:prefLabel "ID" .
  } 
    OPTIONAL {
    ?NLCSObject ?VRKL_LANGProperty ?VRKL_LANG .
    ?VRKL_LANGProperty skos:prefLabel "VRKL_LANG" .
  } 
    bind (strafter(strafter (strafter (?Laagnaam,"-"),"-"),"-") as ?ObjectTotal )
    bind (strafter (strafter (strafter (?Laagnaam,"-"),"-"),"-") as ?ElementPrePre )
    bind ( if ( contains(?ElementPrePre, "-"), strafter(?ElementPrePre, "-"),?ElementPrePre) as ?ElementPre )
    bind ( if ( contains(?ElementPre, "-"), strafter(?ElementPre, "-"),?ElementPre) as ?Element )
    bind ( if ( contains(?ObjectTotal, "_"), strbefore(?ObjectTotal, "_"),strbefore(?ObjectTotal, "-")) as ?Object )
    bind ( if ( contains(?ObjectTotal, "_"), strafter(?ObjectTotal, "_"),strafter(?ObjectTotal, "-")) as ?SubObject01Total )
    bind ( if ( contains(?SubObject01Total, "_"), strbefore(?SubObject01Total, "_"),strbefore(?SubObject01Total, "-")) as ?SubObject01 )
    bind ( if ( contains(?SubObject01Total, "_"), strafter(?SubObject01Total, "_"),strafter(?SubObject01Total, "-")) as ?SubObject02Total )
    bind ( if ( contains(?SubObject02Total, "_"), strbefore(?SubObject02Total, "_"),strbefore(?SubObject02Total, "-")) as ?SubObject02 )
    bind ( if ( contains(?SubObject02Total, "_"), strafter(?SubObject02Total, "_"),strafter(?SubObject02Total, "-")) as ?SubObject03Total )
    bind ( if ( contains(?SubObject03Total, "_"), strbefore(?SubObject03Total, "_"),strbefore(?SubObject03Total, "-")) as ?SubObject03 )
    bind ( if ( contains(?SubObject03Total, "_"), strafter(?SubObject03Total, "_"),strafter(?SubObject03Total, "-")) as ?SubObject04Total )
    bind ( if ( contains(?SubObject04Total, "_"), strbefore(?SubObject04Total, "_"),strbefore(?SubObject04Total, "-")) as ?SubObject04 )
    bind ( if ( contains(?SubObject04Total, "_"), strafter(?SubObject04Total, "_"),strafter(?SubObject04Total, "-")) as ?SubObject05Total )
    bind ( if ( contains(?SubObject05Total, "_"), strbefore(?SubObject05Total, "_"),strbefore(?SubObject05Total, "-")) as ?SubObject05 )
    bind ( if ( contains(?SubObject05Total, "_"), strafter(?SubObject05Total, "_"),strafter(?SubObject05Total, "-")) as ?BewerkingTotal )
    bind ( if ( contains(?BewerkingTotal, "_"), strbefore(?BewerkingTotal, "_"),strbefore(?BewerkingTotal, "-")) as ?Bewerking )
}
</pre>


## Tabel sbibliotheken 
De [query voor sbibliotheken](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20sbibliotheken.rq) resulteert in een tabel zoals deze, rijen en kolommen omgekeerd voor betere weergave in dit document: 


| Abib | bibliotheek | Definitie | ID |
|------|-------------|-----------|----|
| [http://www.example.org/nlcs/def/e9c91921-3dc7-4c10-aba8-2d7e63c275e1](http://www.example.org/nlcs/def/e9c91921-3dc7-4c10-aba8-2d7e63c275e1) | SAL |  |  |
| [http://www.example.org/nlcs/def/de8fd506-a047-4789-a778-2c7c75035294](http://www.example.org/nlcs/def/de8fd506-a047-4789-a778-2c7c75035294) | SAM |  |  |
| [http://www.example.org/nlcs/def/ba6dd0d1-d3b2-4462-9828-3b21da7634c1](http://www.example.org/nlcs/def/ba6dd0d1-d3b2-4462-9828-3b21da7634c1) | SBC |  |  |



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all sbibliotheek info with URI, name, definition, and ID

SELECT DISTINCT ?Abib ?bibliotheek ?Definitie ?ID
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "Symbool" .
  ?Abib rdfs:subClassOf ?NLCSObjectTopConcept .
  ?Abib skos:prefLabel ?bibliotheek .
  OPTIONAL { ?Abib skos:definition ?Definitie . }
  OPTIONAL { ?Abib dcterms:identifier ?ID . }  # Assuming dcterms:identifier is used for the ID
}
ORDER BY ?bibliotheek
</pre>



## Tabel status 
De [query voor status](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20status.rq) resulteert in een tabel zoals deze:

| StatusURI | Status | Omschrijving |
|-----------|--------|---------------|
| http://www.example.org/nlcs/def/0bafa9e2-958b-47b2-aed6-6fd7d2cbcec4 | B | bestaand werk |
| http://www.example.org/nlcs/def/4ebfb892-cb19-40df-a3d9-91b1d21248c7 | N | nieuw werk |
| http://www.example.org/nlcs/def/d53156ad-de5a-4436-9720-c2bf82d09a12 | R | revisie |



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?StatusURI ?Status ?Omschrijving 
WHERE { 
    ?statusTopConcept skos:prefLabel "Statussen" .
    ?StatusURI a ?statusTopConcept ;
        skos:prefLabel ?Status ;
        skos:definition ?Omschrijving .
} 
ORDER BY ?Status
</pre>

<div class="issue" data-number="291"></div>



## Tabel symbolen
De [query voor symbolen](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20symbolen.rq) resulteert in een tabel zoals deze:

| | Symbool 1 | Symbool 2 | Symbool 3 |
|---|---|---|---|
| Symbool | http://www.example.org/nlcs/def/628f9329-ec1f-47d4-9aeb-60b4c1895695 | http://www.example.org/nlcs/def/b7d51e67-1bb0-4244-bffb-88a7e5934ebc | http://www.example.org/nlcs/def/e3329046-59d7-425b-9f12-9a8684c4930e |
| FASE | B | B | B |
| sBIB | SAL | SGR | SGR |
| SYMBOOL | B-SAL-VERWIJZING_MATERIAALNUMMER-SO | B-SGR-BOOM_1E GROOTTE | B-SGR-BOOM_2E GROOTTE |
| SYMBOOL_alt | SAL:B-SAL-VERWIJZING_MATERIAALNUMMER | SGR:B-SGR-BOOM_1E GROOTT | SGR:B-SGR-BOOM_2E GROOTT |
| ID | 3004 | 3031 | 3032 |
| opt | SO |  |  |
| STATUS |  |  |  |
| VRKL_KORT |  |  |  |
| VRKL_LANG |  |  |  |



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Symbool information including URIs and stores it in the familiar Excel/CSV format

SELECT DISTINCT ?Symbool ?FASE ?sBIB ?SYMBOOL ?SYMBOOL_alt ?ID ?opt ?STATUS ?VRKL_KORT ?VRKL_LANG
WHERE {
  ?SymboolTopConcept a owl:Class .
  ?SymboolTopConcept skos:prefLabel "Symbool" .
  ?Sbib rdfs:subClassOf ?SymboolTopConcept .
  ?Sbib skos:prefLabel ?sBIB .
  ?Symbool rdfs:subClassOf+ ?Sbib .
  ?Symbool skos:prefLabel ?SYMBOOL .
  OPTIONAL { ?Symbool skos:altLabel ?SYMBOOL_alt . }
  OPTIONAL {
    ?Symbool rdfs:subClassOf ?bnodeStatus .
    ?bnodeStatus owl:onProperty ?StatusProperty .
    ?StatusProperty skos:prefLabel "Status" .
    OPTIONAL { ?StatusProperty owl:hasValue/skos:prefLabel ?StatusValue . }
    BIND( COALESCE(?StatusValue, "*") AS ?STATUS) .
  }
  FILTER (CONTAINS(STR(?SYMBOOL), STR(CONCAT(STR(?sBIB), "-")))) 
  OPTIONAL {
    BIND (STRBEFORE(STR(?SYMBOOL), STR(CONCAT(CONCAT("-", STR(?sBIB)), "-" ))) AS ?FASE2) . 
    BIND (IF(CONTAINS(?FASE2, "-"), STRBEFORE(?FASE2, "-"), ?FASE2) AS ?FASE) 
  }
  OPTIONAL {
    BIND (STRAFTER(?SYMBOOL, CONCAT(STR(?sBIB), "-")) AS ?opt2) 
    BIND (IF(CONTAINS(?opt2, "-"), STRAFTER(?opt2, "-"), ?opt2) AS ?opt3)
    BIND (IF(CONTAINS(?opt3, "-"), STRAFTER(?opt3, "-"), ?opt3) AS ?opt4)
    BIND (IF(STRLEN(?opt4) > 4, "", ?opt4) AS ?opt)
  } 
  OPTIONAL {
    ?Symbool ?IDProperty ?ID .
    ?IDProperty skos:prefLabel "ID" .
  }
  OPTIONAL {
    ?Symbool ?VRKL_KORTProperty ?VRKL_KORT .
    ?VRKL_KORTProperty skos:prefLabel "VRKL_KORT" .
  } 
  OPTIONAL {
    ?Symbool ?VRKL_LANGProperty ?VRKL_LANG .
    ?VRKL_LANGProperty skos:prefLabel "VRKL_LANG" .
  }
}
ORDER BY ?SYMBOOL
</pre>

<div class="issue" data-number="283"></div>

<div class="issue" data-number="284"></div>


## Wijzigingen tussen versies

<div class="issue" data-number="149"></div>

De [query voor ...]() resulteert in een objectentabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
</pre>



## Tabel bgt mapping
De [query voor ...]() resulteert in een tabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
</pre>

## Tabel GWSW mapping

De [query voor ...]() resulteert in een tabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
</pre>




