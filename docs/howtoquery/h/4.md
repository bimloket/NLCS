# Query's

 
## Tabel abibliotheken

de abibliotheken zijn de "arceringsbibliotheken", oftewel de NLCS hoofdgroepen voor arceringen.

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Arceringen bibliotheek information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?abibliotheekURI ?id ?abibliotheek ?omschrijving
WHERE {
  ?ArceringTopConcept a owl:Class .
  ?ArceringTopConcept skos:prefLabel "Arcering" .
  ?abibliotheekURI rdfs:subClassOf ?ArceringTopConcept .
  ?abibliotheekURI skos:prefLabel ?abibliotheek .
  ?abibliotheekURI skos:definition ?omschrijving .
  OPTIONAL {
    ?abibliotheekURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  } 
} ORDER BY ?id
</pre>

De [query voor abibliotheken](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Abibliotheken.rq) resulteert in een tabel zoals deze:

| abibliotheekURI                                                      | id | abibliotheek | omschrijving |
|--------------------------------------------------------------------------------|----|-----|------------|
| https://www.example.org/nlcs/def/02ffcdec-87e9-4ea3-8aa6-7ae6d32470c4 | 1   | ACO |     Constructies       |
| https://www.example.org/nlcs/def/95d72fe5-9222-417d-a5d6-dcc9b296a9e4 |  2  | AGR |    Groen        |
| https://www.example.org/nlcs/def/00741035-a28a-4e9d-89e1-c581c7fdb8a5 |   3 | AGW |      Grondwerk      |



## Tabel arceringen



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Arcering information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?arceringURI ?id ?abibliotheekURI ?abibliotheek ?fase ?arcering ?optie ?schaal ?vrkl_kort ?vrkl_lang ?fileURL
WHERE {
  ?ArceringTopConcept a owl:Class .
  ?ArceringTopConcept skos:prefLabel "Arcering" .
  ?abibliotheekURI rdfs:subClassOf ?ArceringTopConcept .
  ?abibliotheekURI skos:definition ?omschrijving .
  ?abibliotheekURI skos:prefLabel ?abibliotheek .
  ?arceringURI rdfs:subClassOf+ ?abibliotheekURI .
  ?arceringURI skos:prefLabel ?arcering .
  FILTER( NOT EXISTS { ?subArcering rdfs:subClassOf ?arceringURI . }) .
  OPTIONAL {
    ?arceringURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  } 
  OPTIONAL {
    ?arceringURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  } 
  OPTIONAL {
    ?arceringURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  } 
  OPTIONAL {
    ?arceringURI ?vrkl_kortProperty ?vrkl_kort .
    ?vrkl_kortProperty skos:prefLabel "VRKL_KORT" .
  } 
  OPTIONAL {
    ?arceringURI ?vrkl_langProperty ?vrkl_lang .
    ?vrkl_langProperty skos:prefLabel "VRKL_LANG" .
  }
  OPTIONAL {
    ?arceringURI ?schaalProperty ?schaal .
    ?schaalProperty skos:prefLabel "Schaal" .
  } 
  OPTIONAL {
    ?arceringURI ?urlProperty ?fileURL .
    ?urlProperty skos:prefLabel "URL" .
  } 
} ORDER BY ?id
</pre>

De [query voor arceringen](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Arceringen.rq) resulteert in een tabel zoals deze, gespiegeld voor betere leesbaarheid (de inhoud klopt niet):

|                           | Row 1                                                  | Row 2                                                  | Row 3                                                  | Row 4                                                  |
|---------------------------|--------------------------------------------------------|--------------------------------------------------------|--------------------------------------------------------|--------------------------------------------------------|
| **arceringURI**                 |  https://www.example.org/nlcs/def/55c1f87b-535c-4532-ace8-9e62694488db | hhttps://www.example.org/nlcs/def/37d2c991-aebd-4df0-8c12-8361d4df6cd8 | hhttps://www.example.org/nlcs/def/338b9a47-db06-4856-91df-01ef09f3d7e51 | hhttps://www.example.org/nlcs/def/37868743-baf2-4572-a427-9f460de5725a |  
| **id**                    | 19                                                     | 20                                                     | 18                                                     | 12                                                     |
| abibliotheekURI                 |  https://www.example.org/nlcs/def/55c1f87b-535c-4532-ace8-9e62694488db1 | hhttps://www.example.org/nlcs/def/37d2c991-aebd-4df0-8c12-8361d4df6cd81 | hhttps://www.example.org/nlcs/def/338b9a47-db06-4856-91df-01ef09f3d7e5 | hhttps://www.example.org/nlcs/def/37868743-baf2-4572-a427-9f460de5725a |  
| **abibliotheek**                   | ACO                                                    | ACO                                                    | ACO                                                    | ACO                                                    |
| **fase**              |                B                                       |                                                        |                                                        |                                                        |
| **arcering**              | ACO-HOUT_02-SODMM                                      | ACO-HOUT_03-SODMM                                      | ACO-HOUT_01-SODMM                                      | ACO-BETON_VOORGESP_PREFAB-SODMM                        |
| **optie**                   | SODMM                                                  | SODMM                                                  | SODMM                                                  | SODMM                                                  |
| **schaal**              |       1                                                |              1                                          |             1                                           |                                                        |
| **vrkl_kort**             | hout02                                                 | hout03                                                 | hout01                                                 | prefabbeton                                            |
| **vrkl_lang**             | 45,0,0,0,0.2                                           | 0,0,0,0,0.3                                            | 45,0,0,0,1.875 135,0,0,3.75,1.875,4.6875,-2.8125       | 45,0,0,0,2.5 -45,0,0,0,2.5 0,0,0,0,.75,0,-0.75        |
| **fileURL**                |  https://www.example.org/nlcs/def/55c1f87b-535c-4532-ace8-9e62694488db2 | hhttps://www.example.org/nlcs/def/37d2c991-aebd-4df0-8c12-8361d4df6cd82 | hhttps://www.example.org/nlcs/def/338b9a47-db06-4856-91df-01ef09f3d7e52 | hhttps://www.example.org/nlcs/def/37868743-baf2-4572-a427-9f460de5725a |  

## Tabel disciplines

De query voor disciplines ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Discipline information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?disciplineURI ?id ?code ?verklaring ?omschrijving
WHERE {
  ?DisciplineTopConcept skos:prefLabel "Disciplines" .
  ?disciplineURI a ?DisciplineTopConcept .
  ?disciplineURI skos:prefLabel ?code .
  ?disciplineURI skos:altLabel ?verklaring .
  ?disciplineURI skos:definition ?omschrijving .
  OPTIONAL {
    ?disciplineURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  }
} ORDER BY ?id
</pre>

De [query voor disciplines](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Disciplines.rq) resulteert in een tabel zoals deze:

| disciplineURI|  id   |   code | verklaring |  omschrijving
|------------|------|-------------|
| http://www.example.org/nlcs/def/4adb2e90-c529-450e-ad37-fe254e218e90 |   8   |   CO | Vorm- en wapeningstekeningen van civieltechnische constructies, zoals: brug, tunnel, onderdoorgang, viaduct, sluizencomplex, spoortunnel, faunapassage. |



## Hoofdgroepen


De query voor hoofdgroepen ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Hoofdgroep information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?hoofdgroepURI ?id ?hoofdgroep ?afkorting
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  ?hoofdgroepURI rdfs:subClassOf ?NLCSObjectTopConcept .
  ?hoofdgroepURI skos:prefLabel ?afkorting .
  ?hoofdgroepURI skos:definition ?hoofdgroep .
  OPTIONAL {
    ?hoofdgroepURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  } 
} ORDER BY ?id
</pre>

De [query voor hoofdgroepen](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Hoofdgroepen.rq) resulteert in een tabel zoals deze:

| hoofdgroepURI                                                       | id | hoofdgroep                  | afkorting |
|---------------------------------------------------------------------|----|-----------------------------|-----------|
| http://www.example.com/nlcs/def/5b0d986a-88d3-4bb4-bc6f-fe940836c242 | 1  | Voor alle hoofdgroepen      | ZZ        |
| http://www.example.com/nlcs/def/6c4b8ac2-7a72-4ae6-b169-4dec77be3f02 | 10 | Kadastrale informatie en Grenzen | KG        |
| http://www.example.com/nlcs/def/cf3d9f17-91ab-409b-be8b-25391d045637 | 11 | Kabels en Leidingen         | KL       




## Objectentabellen per hoofdgroep

De [query objectentabellen per hoofdgroep](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Objects.rq) ziet er als volgt uit, merk op dat de database zo groot is, dat een query van alle objecten in alle hoofdgroepen niet mogelijk is, je kunt deze informatie alleen per hoofdgroep opvragen. In deze query komen ook de URI's en de relaties met arceringen en symbolen terug, waardoor er extra rijen ontstaan als je dit in CSV formaat opvraagt. Daarom is er ook een verkorte query, zonder die onderdelen.

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Object information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?objectURI ?hoofdgroepURI ?hoofdgroep ?id_nummer ?omschrijving ?kind_vanURI ?kind_van ?status ?discipline
?lw_bURI ?lw_b ?kl_bURI ?kl_b ?kl_b_aURI ?kl_b_a ?kl_b_gdURI ?kl_b_gd ?kl_b_gnURI ?kl_b_gn ?kl_b_vURI ?kl_b_v ?lt_bURI ?lt_b
?lw_nURI ?lw_n ?kl_nURI ?kl_n ?kl_n_aURI ?kl_n_a ?kl_n_gdURI ?kl_n_gd ?kl_n_gnURI ?kl_n_gn ?kl_n_vURI ?kl_n_v ?lt_nURI ?lt_n
?lw_tURI ?lw_t ?kl_tURI ?kl_t ?kl_t_aURI ?kl_t_a ?kl_t_gdURI ?kl_t_gd ?kl_t_gnURI ?kl_t_gn ?kl_t_vURI ?kl_t_v ?lt_tURI ?lt_t
?lw_vURI ?lw_v ?kl_vURI ?kl_v ?kl_v_aURI ?kl_v_a ?kl_v_gdURI ?kl_v_gd ?kl_v_gnURI ?kl_v_gn ?kl_v_vURI ?kl_v_v ?lt_vURI ?lt_v
?element ?sobjectURI ?sobject ?aobjectURI ?aobject ?vrkl_kort
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  ?hoofdgroepURI rdfs:subClassOf ?NLCSObjectTopConcept .
  ?hoofdgroepURI skos:prefLabel ?hoofdgroep .
  ?objectURI rdfs:subClassOf+ ?hoofdgroepURI .
  ?objectURI skos:prefLabel ?omschrijving .
  FILTER (?hoofdgroep = "AL") .  # INPUT REQUIRED - Change value to the desired hoofdgroep, AL is used as example
  OPTIONAL {
    ?objectURI ?idProperty ?id_nummer .
    ?idProperty skos:prefLabel "ID" .
  } 
  OPTIONAL {
    ?objectURI rdfs:subClassOf ?kind_vanURI .
    FILTER (?kind_vanURI != ?hoofdgroepURI) .
    ?kind_vanURI ?idProperty ?kind_van .
    ?idProperty skos:prefLabel "ID" .
  }
  OPTIONAL {
    ?objectURI ?elementProperty ?element .
    ?elementProperty skos:prefLabel "Element" .
  } 
  OPTIONAL {
    ?objectURI ?vrkl_kortProperty ?vrkl_kort .
    ?vrkl_kortProperty skos:prefLabel "VRKL_KORT" .
  } 

  ?objectURI rdfs:subClassOf ?bnodeStatus .
  ?bnodeStatus owl:onProperty ?StatusProperty .
  ?StatusProperty skos:prefLabel "Status" .
  OPTIONAL {
    ?bnodeStatus owl:hasValue/skos:prefLabel ?StatusValue .
  }
  BIND( COALESCE(?StatusValue, "*") AS ?status) .
  ?objectURI rdfs:subClassOf ?bnodeDiscipline .
  ?bnodeDiscipline owl:onProperty ?DisciplineProperty .
  ?DisciplineProperty skos:prefLabel "Discipline" .
  OPTIONAL {
    ?bnodeDiscipline owl:hasValue/skos:prefLabel ?DisciplineValue .
  }
  BIND( COALESCE(?DisciplineValue, "**") AS ?discipline) .

  OPTIONAL {
    ?objectURI rdfs:subClassOf ?bnodeSymbool .
    ?bnodeSymbool owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
    ?bnodeSymbool owl:onClass ?sobjectURI .
    ?sobjectURI rdfs:subClassOf* ?SymboolTopConcept .
    ?SymboolTopConcept skos:prefLabel "Symbool" .
    ?sobjectURI skos:prefLabel ?sobject .
  }
  OPTIONAL {
    ?objectURI rdfs:subClassOf ?bnodeArcering .
    ?bnodeArcering owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
    ?bnodeArcering owl:onClass ?aobjectURI .
    ?aobjectURI rdfs:subClassOf* ?SymboolTopConcept .
    ?ArceringTopConcept skos:prefLabel "Arcering" .
    ?aobjectURI skos:prefLabel ?aobject .
  }
  OPTIONAL {
    ?objectURI ?elementProperty ?element .
    ?elementProperty skos:prefLabel "VRKL_KORT" .
  } 

  OPTIONAL {
    ?objectURI ?BLineweightProperty ?lw_bURI .
    ?BLineweightProperty skos:prefLabel "B lineweight" .
    ?lw_bURI skos:prefLabel ?lw_b .

    ?objectURI ?BColorProperty ?kl_bURI_temp .
    ?BColorProperty skos:prefLabel "B color" .
    OPTIONAL {
      ?kl_bURI_temp skos:prefLabel ?kl_b_temp .
    }
    BIND( COALESCE(?kl_b_temp, ?kl_bURI_temp) AS ?kl_b) .
    BIND( IF(?kl_b = ?kl_b_temp, ?kl_bURI_temp, ?undefined) AS ?kl_bURI) .

    ?objectURI ?BColorAProperty ?kl_b_aURI_temp .
    ?BColorAProperty skos:prefLabel "B color A" .
    OPTIONAL {
      ?kl_b_aURI_temp skos:prefLabel ?kl_b_a_temp .
    }
    BIND( COALESCE(?kl_b_a_temp, ?kl_b_aURI_temp) AS ?kl_b_a) .
    BIND( IF(?kl_b_a = ?kl_b_a_temp, ?kl_b_aURI_temp, ?undefined) AS ?kl_b_aURI) .

    ?objectURI ?BColorGDProperty ?kl_b_gdURI_temp .
    ?BColorGDProperty skos:prefLabel "B color GD" .
    OPTIONAL {
      ?kl_b_gdURI_temp skos:prefLabel ?kl_b_gd_temp .
    }
    BIND( COALESCE(?kl_b_gd_temp, ?kl_b_gdURI_temp) AS ?kl_b_gd) .
    BIND( IF(?kl_b_gd = ?kl_b_gd_temp, ?kl_b_gdURI_temp, ?undefined) AS ?kl_b_gdURI) .

    ?objectURI ?BColorGNProperty ?kl_b_gnURI_temp .
    ?BColorGNProperty skos:prefLabel "B color GN" .
    OPTIONAL {
      ?kl_b_gnURI_temp skos:prefLabel ?kl_b_gn_temp .
    }
    BIND( COALESCE(?kl_b_gn_temp, ?kl_b_gnURI_temp) AS ?kl_b_gn) .
    BIND( IF(?kl_b_gn = ?kl_b_gn_temp, ?kl_b_gnURI_temp, ?undefined) AS ?kl_b_gnURI) .

    ?objectURI ?BColorVProperty ?kl_b_vURI_temp .
    ?BColorVProperty skos:prefLabel "B color V" .
    OPTIONAL {
      ?kl_b_vURI_temp skos:prefLabel ?kl_b_v_temp .
    }
    BIND( COALESCE(?kl_b_v_temp, ?kl_b_vURI_temp) AS ?kl_b_v) .
    BIND( IF(?kl_b_v = ?kl_b_v_temp, ?kl_b_vURI_temp, ?undefined) AS ?kl_b_vURI) .

    ?objectURI ?BLinetypeProperty ?lt_bURI .
    ?BLinetypeProperty skos:prefLabel "B linetype" .
    ?lt_bURI skos:prefLabel ?lt_b .
  } 
  OPTIONAL {
    ?objectURI ?NLineweightProperty ?lw_nURI .
    ?NLineweightProperty skos:prefLabel "N lineweight" .
    ?lw_nURI skos:prefLabel ?lw_n .

    ?objectURI ?NColorProperty ?kl_nURI_temp .
    ?NColorProperty skos:prefLabel "N color" .
    OPTIONAL {
      ?kl_nURI_temp skos:prefLabel ?kl_n_temp .
    }
    BIND( COALESCE(?kl_n_temp, ?kl_nURI_temp) AS ?kl_n) .
    BIND( IF(?kl_n = ?kl_n_temp, ?kl_nURI_temp, ?undefined) AS ?kl_nURI) .

    ?objectURI ?NColorAProperty ?kl_n_aURI_temp .
    ?NColorAProperty skos:prefLabel "N color A" .
    OPTIONAL {
      ?kl_n_aURI_temp skos:prefLabel ?kl_n_a_temp .
    }
    BIND( COALESCE(?kl_n_a_temp, ?kl_n_aURI_temp) AS ?kl_n_a) .
    BIND( IF(?kl_n_a = ?kl_n_a_temp, ?kl_n_aURI_temp, ?undefined) AS ?kl_n_aURI) .

    ?objectURI ?NColorGDProperty ?kl_n_gdURI_temp .
    ?NColorGDProperty skos:prefLabel "N color GD" .
    OPTIONAL {
      ?kl_n_gdURI_temp skos:prefLabel ?kl_n_gd_temp .
    }
    BIND( COALESCE(?kl_n_gd_temp, ?kl_n_gdURI_temp) AS ?kl_n_gd) .
    BIND( IF(?kl_n_gd = ?kl_n_gd_temp, ?kl_n_gdURI_temp, ?undefined) AS ?kl_n_gdURI) .

    ?objectURI ?NColorGNProperty ?kl_n_gnURI_temp .
    ?NColorGNProperty skos:prefLabel "N color GN" .
    OPTIONAL {
      ?kl_n_gnURI_temp skos:prefLabel ?kl_n_gn_temp .
    }
    BIND( COALESCE(?kl_n_gn_temp, ?kl_n_gnURI_temp) AS ?kl_n_gn) .
    BIND( IF(?kl_n_gn = ?kl_n_gn_temp, ?kl_n_gnURI_temp, ?undefined) AS ?kl_n_gnURI) .

    ?objectURI ?NColorVProperty ?kl_n_vURI_temp .
    ?NColorVProperty skos:prefLabel "N color V" .
    OPTIONAL {
      ?kl_n_vURI_temp skos:prefLabel ?kl_n_v_temp .
    }
    BIND( COALESCE(?kl_n_v_temp, ?kl_n_vURI_temp) AS ?kl_n_v) .
    BIND( IF(?kl_n_v = ?kl_n_v_temp, ?kl_n_vURI_temp, ?undefined) AS ?kl_n_vURI) .

    ?objectURI ?NLinetypeProperty ?lt_nURI .
    ?NLinetypeProperty skos:prefLabel "N linetype" .
    ?lt_nURI skos:prefLabel ?lt_n .
  } 
  OPTIONAL {
    ?objectURI ?TLineweightProperty ?lw_tURI .
    ?TLineweightProperty skos:prefLabel "T lineweight" .
    ?lw_tURI skos:prefLabel ?lw_t .

    ?objectURI ?TColorProperty ?kl_tURI_temp .
    ?TColorProperty skos:prefLabel "T color" .
    OPTIONAL {
      ?kl_tURI_temp skos:prefLabel ?kl_t_temp .
    }
    BIND( COALESCE(?kl_t_temp, ?kl_tURI_temp) AS ?kl_t) .
    BIND( IF(?kl_t = ?kl_t_temp, ?kl_tURI_temp, ?undefined) AS ?kl_tURI) .

    ?objectURI ?TColorAProperty ?kl_t_aURI_temp .
    ?TColorAProperty skos:prefLabel "T color A" .
    OPTIONAL {
      ?kl_t_aURI_temp skos:prefLabel ?kl_t_a_temp .
    }
    BIND( COALESCE(?kl_t_a_temp, ?kl_t_aURI_temp) AS ?kl_t_a) .
    BIND( IF(?kl_t_a = ?kl_t_a_temp, ?kl_t_aURI_temp, ?undefined) AS ?kl_t_aURI) .

    ?objectURI ?TColorGDProperty ?kl_t_gdURI_temp .
    ?TColorGDProperty skos:prefLabel "T color GD" .
    OPTIONAL {
      ?kl_t_gdURI_temp skos:prefLabel ?kl_t_gd_temp .
    }
    BIND( COALESCE(?kl_t_gd_temp, ?kl_t_gdURI_temp) AS ?kl_t_gd) .
    BIND( IF(?kl_t_gd = ?kl_t_gd_temp, ?kl_t_gdURI_temp, ?undefined) AS ?kl_t_gdURI) .

    ?objectURI ?TColorGNProperty ?kl_t_gnURI_temp .
    ?TColorGNProperty skos:prefLabel "T color GN" .
    OPTIONAL {
      ?kl_t_gnURI_temp skos:prefLabel ?kl_t_gn_temp .
    }
    BIND( COALESCE(?kl_t_gn_temp, ?kl_t_gnURI_temp) AS ?kl_t_gn) .
    BIND( IF(?kl_t_gn = ?kl_t_gn_temp, ?kl_t_gnURI_temp, ?undefined) AS ?kl_t_gnURI) .

    ?objectURI ?TColorVProperty ?kl_t_vURI_temp .
    ?TColorVProperty skos:prefLabel "T color V" .
    OPTIONAL {
      ?kl_t_vURI_temp skos:prefLabel ?kl_t_v_temp .
    }
    BIND( COALESCE(?kl_t_v_temp, ?kl_t_vURI_temp) AS ?kl_t_v) .
    BIND( IF(?kl_t_v = ?kl_t_v_temp, ?kl_t_vURI_temp, ?undefined) AS ?kl_t_vURI) .

    ?objectURI ?TLinetypeProperty ?lt_tURI .
    ?TLinetypeProperty skos:prefLabel "T linetype" .
    ?lt_tURI skos:prefLabel ?lt_t .
  }  
  OPTIONAL {
    ?objectURI ?VLineweightProperty ?lw_vURI .
    ?VLineweightProperty skos:prefLabel "V lineweight" .
    ?lw_vURI skos:prefLabel ?lw_v .

    ?objectURI ?VColorProperty ?kl_vURI_temp .
    ?VColorProperty skos:prefLabel "V color" .
    OPTIONAL {
      ?kl_vURI_temp skos:prefLabel ?kl_v_temp .
    }
    BIND( COALESCE(?kl_v_temp, ?kl_vURI_temp) AS ?kl_v) .
    BIND( IF(?kl_v = ?kl_v_temp, ?kl_vURI_temp, ?undefined) AS ?kl_vURI) .

    ?objectURI ?VColorAProperty ?kl_v_aURI_temp .
    ?VColorAProperty skos:prefLabel "V color A" .
    OPTIONAL {
      ?kl_v_aURI_temp skos:prefLabel ?kl_v_a_temp .
    }
    BIND( COALESCE(?kl_v_a_temp, ?kl_v_aURI_temp) AS ?kl_v_a) .
    BIND( IF(?kl_v_a = ?kl_v_a_temp, ?kl_v_aURI_temp, ?undefined) AS ?kl_v_aURI) .

    ?objectURI ?VColorGDProperty ?kl_v_gdURI_temp .
    ?VColorGDProperty skos:prefLabel "V color GD" .
    OPTIONAL {
      ?kl_v_gdURI_temp skos:prefLabel ?kl_v_gd_temp .
    }
    BIND( COALESCE(?kl_v_gd_temp, ?kl_v_gdURI_temp) AS ?kl_v_gd) .
    BIND( IF(?kl_v_gd = ?kl_v_gd_temp, ?kl_v_gdURI_temp, ?undefined) AS ?kl_v_gdURI) .

    ?objectURI ?VColorGNProperty ?kl_v_gnURI_temp .
    ?VColorGNProperty skos:prefLabel "V color GN" .
    OPTIONAL {
      ?kl_v_gnURI_temp skos:prefLabel ?kl_v_gn_temp .
    }
    BIND( COALESCE(?kl_v_gn_temp, ?kl_v_gnURI_temp) AS ?kl_v_gn) .
    BIND( IF(?kl_v_gn = ?kl_v_gn_temp, ?kl_v_gnURI_temp, ?undefined) AS ?kl_v_gnURI) .

    ?objectURI ?VColorVProperty ?kl_v_vURI_temp .
    ?VColorVProperty skos:prefLabel "V color V" .
    OPTIONAL {
      ?kl_v_vURI_temp skos:prefLabel ?kl_v_v_temp .
    }
    BIND( COALESCE(?kl_v_v_temp, ?kl_v_vURI_temp) AS ?kl_v_v) .
    BIND( IF(?kl_v_v = ?kl_v_v_temp, ?kl_v_vURI_temp, ?undefined) AS ?kl_v_vURI) .

    ?objectURI ?VLinetypeProperty ?lt_vURI .
    ?VLinetypeProperty skos:prefLabel "V linetype" .
    ?lt_vURI skos:prefLabel ?lt_v .
  } 
  
} ORDER BY ?id
</pre>


De [query voor de tabellen van de hoofdgroepen](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20hoofdgroepen.rq) resulteert in een tabel zoals deze, de rijen en kolommen zijn omgekeerd ten behoeve van de leesbaarheid in dit document:

| Kenmerk      | Inhoud                                                                                                                             |
|--------------|-----------------------------------------------------------------------------------------------------------------------------------|
| objectURI    | [http://www.example.com/nlcs/def/cf01cca0-27f9-40d1-8c1f-bc45a5f2bc18](http://www.example.com/nlcs/def/cf01cca0-27f9-40d1-8c1f-bc45a5f2bc18) |
| hoofdgroepURI  | [http://www.example.com/nlcs/def/0a74f373-7382-405d-99ec-50e44e6483fc](http://www.example.com/nlcs/def/0a74f373-7382-405d-99ec-50e44e6483fc) |
| hoofdgroep   | RI                                                                                                                              |
| id_nummer    | 3487                                                                                                                            |
| omschrijving   | GELEIDECONSTRUCTIE_GELEIDERAIL_KOPPELING_SCHILDMUUR                                                                                  |
| kind_vanURI   | [http://www.example.com/nlcs/def/e6fd30ed-ed03-4863-86ca-12f7abbbc53a](http://www.example.com/nlcs/def/e6fd30ed-ed03-4863-86ca-12f7abbbc53a) |
| kind_van      | 220                                                                                                                               |
| status          | *                                                                                                                                |
| discipline    | **                                                                                                                                  |
| lw_bURI       | [http://www.example.com/nlcs/def/bacc787a-3288-4f27-a233-6a58ce3d5c65](http://www.example.com/nlcs/def/bacc787a-3288-4f27-a233-6a58ce3d5c65) |
| lw_b              | 0.18                                                                                                                             |
| kl_bURI       | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_b            | 20                                                                                                                               |
| kl_b_aURI   | [http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec](http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec) |
| kl_b_a           | 22                                                                                                                              |
| kl_b_gdURI  | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_b_gd        | 20                                                                                                                                |
| kl_b_gnURI  | [http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3](http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3) |
| kl_b_gn        | 253                                                                                                                              |
| kl_b_vURI    | [http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437](http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437) |
| kl_b_v           | CONTINUOUS                                                                                                                    |
| lt_bURI        | [http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4](http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4) |
| lw_nURI       | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| lw_n            | 20                                                                                                                               |
| kl_nURI        | [http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec](http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec) |
| kl_n             | 22                                                                                                                               |
| kl_n_aURI     | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_n_a           | 20                                                                                                                               |
| kl_n_gdURI   | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_n_gd        | 20                                                                                                                               |
| kl_n_gnURI   | [http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3](http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3) |
| kl_n_gn         | 253                                                                                                                               |
| kl_n_vURI     | [http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437](http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437) |
| kl_n_v            | CONTINUOUS                                                                                                                     |
| lt_nURI        | [http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4](http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4) |
| lw_tURI        | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| lw_t             | 20                                                                                                                               |
| kl_tURI         | [http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec](http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec) |
| kl_t              | 22                                                                                                                               |
| kl_t_aURI      | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_t_a             | 20                                                                                                                               |
| kl_t_gdURI    | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_t_gd            | 20                                                                                                                               |
| kl_t_gnURI   | [http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3](http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3) |
| kl_t_gn           | 253                                                                                                                               |
| kl_t_vURI       | [http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437](http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437) |
| kl_t_v                | CONTINUOUS                                                                                                                    |
| lt_tURI        | [http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4](http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4) |
| lw_vURI       | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| lw_v            | 20                                                                                                                                  |
| kl_vURI        | [http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec](http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec) |
| kl_v             | 22                                                                                                                                 |
| kl_v_aURI     | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_v_a          | 20                                                                                                                               |
| kl_v_gdURI   | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_v_gd           | 20                                                                                                                               |
| kl_v_gnURI   | [http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3](http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3) |
| kl_v_gn           | 253                                                                                                                               |
| kl_v_vURI        | [http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437](http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437) |
| kl_v_v               | CONTINUOUS                                                                                                                     |
| lt_vURI          | [http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4](http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4) |
| element           | G/S                                                                                                                               |
| sobjectURI       | [http://www.example.com/nlcs/def/6d076150-6701-4fec-bff9-a8489ce32bd4](http://www.example.com/nlcs/def/6d076150-6701-4fec-bff9-a8489ce32bd4) |
| sobject           | SRI-SCHILDMUUR                                                                                                                    |
| aobjectURI       | [http://www.example.com/nlcs/def/6d076150-6701-4fec-bff9-a8489ce32bd4](http://www.example.com/nlcs/def/6d076150-6701-4fec-bff9-a8489ce32bd4) |
| aobject           | SRI-SCHILDMUUR                                                                                                                     |
| vrkl_kort         | ""                                                                                                                                    |

## Verkorte objectentabellen per hoofdgroep
De [Verkortequeryobjectentabel](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Objects_verkort) is bedoeld voor het genereren van leesbare objectentabellen ten behoeve van de inhoudelijk experts die aan de standaard werken. Deze bevatten geen relaties met symbolen en arceringen, zodat er geen dubbele regels per object ontstaan. En geen URI's, omdat die niet voor menselijke lezers bedoeld zijn.



## Tabel lijnkleuren
De [query voor lijnkleuren](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Lijnkleuren.rq) ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Lijnkleur information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?lijnkleurURI ?id ?fase ?nummer ?hex ?R ?G ?B ?optie
WHERE {
  ?LijnkleurTopConcept a owl:Class .
  ?LijnkleurTopConcept skos:prefLabel "Lijnkleur" . 
  ?lijnkleurURI rdfs:subClassOf ?LijnkleurTopConcept .
  ?lijnkleurURI skos:prefLabel ?nummer .
  OPTIONAL {
  	?lijnkleurURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  }
  OPTIONAL {
  	?lijnkleurURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  }
  OPTIONAL {
  	?lijnkleurURI ?hexProperty ?hex .
    ?hexProperty skos:prefLabel "HEX" .
  }
  OPTIONAL {
  	?lijnkleurURI ?RProperty ?R .
    ?RProperty skos:prefLabel "R" .
  }
  OPTIONAL {
  	?lijnkleurURI ?GProperty ?G .
    ?GProperty skos:prefLabel "G" .
  }
  OPTIONAL {
  	?lijnkleurURI ?BProperty ?B .
    ?BProperty skos:prefLabel "B" .
  }
  OPTIONAL {
  	?lijnkleurURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  }
} ORDER BY ?id
</pre>

De query voor lijnkleuren resulteert in een tabel zoals deze:

| lijnkleurURI                                                       | id  | fase | nummer | hex     | R   | G   | B   | optie |
|--------------------------------------------------------------------|-----|------|--------|---------|-----|-----|-----|-------|
| http://www.example.com/nlcs/def/40a13afa-d228-461a-981f-87e23784a038 | 100 | 0    | 0      | #000000 | 0   | 0   | 0   |       |
| http://www.example.com/nlcs/def/89970413-24e9-4574-a5df-d6a572d13fac | 101 | 0    | 1      | #FF0000 | 255 | 0   | 0   |       |
| http://www.example.com/nlcs/def/af48781d-0a36-4244-88ff-cb86c15b5313 | 102 | 0    | 2      | #FFFF00 | 255 | 255 | 0   |       |


## Tabel lijntypes


De [query voor lijntypes](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Lijntypes.rq) ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Lijntype information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?lijntypeURI ?id ?hoofdgroep ?omschrijving ?fase ?optie ?autocaddef ?fileURL
WHERE {
  ?LijntypeTopConcept a owl:Class .
  ?LijntypeTopConcept skos:prefLabel "Lijntype" .
  ?Hoofdgroep rdfs:subClassOf ?LijntypeTopConcept .
  ?Hoofdgroep skos:prefLabel ?HoofdgroepName .
  ?lijntypeURI rdfs:subClassOf* ?Hoofdgroep .
  ?lijntypeURI skos:prefLabel ?omschrijving .
  BIND ( IF(?lijntypeURI = ?Hoofdgroep, IF(?omschrijving IN ("V-CONTINUOUS-SO", "CONTINUOUS", "NLCS_HIDDEN-SO"), ?lijntypeURI, ?undefined), ?lijntypeURI) AS ?NotHoofdgroep) .
  BIND ( IF(?lijntypeURI = ?Hoofdgroep, IF(?omschrijving IN ("V-CONTINUOUS-SO", "CONTINUOUS", "NLCS_HIDDEN-SO"), ?undefined, ?HoofdgroepName), ?HoofdgroepName) AS ?hoofdgroep) .
  FILTER (?lijntypeURI = ?NotHoofdgroep) .
  OPTIONAL {
    ?lijntypeURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  } OPTIONAL {
    ?lijntypeURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  } OPTIONAL {
    ?lijntypeURI skos:definition ?autocaddef .
  } OPTIONAL {
    ?lijntypeURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  }
  OPTIONAL {
    ?lijntypeURI ?urlProperty ?fileURL .
    ?urlProperty skos:prefLabel "URL" .
  } 
} ORDER BY ?id
</pre>

De query voor lijntypes resulteert in een tabel zoals deze, gespiegeld voor betere leesbaarheid in ReSpec:

| Kenmerk         | Inhoud                                                                                                                         |
|----------------|---------------------------------------------------------------------------------------------------------------------------------|
| lijntypeURI     | [http://www.example.com/nlcs/def/e6abd6a9-f196-4cb6-8a31-21176939ff74](http://www.example.com/nlcs/def/e6abd6a9-f196-4cb6-8a31-21176939ff74) |
| id              | 116                                                                                                                             |
| hoofdgroep      | KG                                                                                                                               |
| omschrijving    | V-KG-KADASTRAAL-SO                                                                                                               |
| fase            | V                                                                                                                                 |
| optie           | SO                                                                                                                                 |
| autocaddef       | A,5.5,[VERWIJDEREN2,NLCS.SHX,x=-2.75,s=0.6],-2,[K,standard,U=0.0,y=-.9,s=1.8],-2,5.5,[VERWIJDEREN2,NLCS.SHX,x=-2.75,s=0.6],-2,[KRUISJE,NLCS.SHX,s=0.75],-2  |
| fileURL          | ""                                                                                                                                  |




## Tabel lijnweights
De [query voor lijndiktes](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20lineweights.rq) resulteert in een tabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Lijnweight information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?lijnweightURI ?id ?omschrijving ?lijngroep35 ?dunner ?dikker
WHERE {
  ?LijnweightTopConcept a owl:Class .
  ?LijnweightTopConcept skos:prefLabel "Lijnweight" . 
  ?lijnweightURI rdfs:subClassOf ?LijnweightTopConcept .
  OPTIONAL {
  	?lijnweightURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  }
  OPTIONAL {
  	?lijnweightURI skos:prefLabel ?omschrijving .
  }
  OPTIONAL {
  	?lijnweightURI ?lijngroep35Property ?lijngroep35 .
    ?lijngroep35Property skos:prefLabel "Lijngroep35" .
  }
  OPTIONAL {
  	?lijnweightURI ?dunnerProperty ?dunner .
    ?dunnerProperty skos:prefLabel "Dunner" .
  }
  OPTIONAL {
  	?lijnweightURI ?dikkerProperty ?dikker .
    ?dikkerProperty skos:prefLabel "Dikker" .
  }
} ORDER BY ?id
</pre>

De query resulteert in deze tabel, gespiegeld voor leesbaarheid:

| lijnweightURI                                                           | http://www.example.com/nlcs/def/4ac13ce8-5524-44d2-8ed4-de62452a9027 |
|-------------------------------------------------------------------------|----------------------------------------------------------------------------------|
| "id"                                                                    | "1"                                                                              |
| "omschrijving"                                                          | "0.18"                                                                           |
| "lijngroep35"                                                           | "X"                                                                               |
| "dunner"                                                                | "0.13"                                                                               |
| "dikker"                                                                | "0.25"                                                                               |




## Tabel sbibliotheken 
De [query voor sbibliotheken](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Sbibliotheken) ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Symbool bibliotheek information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?sbibliotheekURI ?id ?sbibliotheek ?omschrijving
WHERE {
  ?SymboolTopConcept a owl:Class .
  ?SymboolTopConcept skos:prefLabel "Symbool" .
  ?sbibliotheekURI rdfs:subClassOf ?SymboolTopConcept .
  ?sbibliotheekURI skos:prefLabel ?sbibliotheek .
  ?sbibliotheekURI skos:definition ?omschrijving .
  OPTIONAL {
    ?sbibliotheekURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  }
} ORDER BY ?id 
</pre>

De query voor sbibliotheken resulteert in een tabel zoals deze: 
| sbibliotheekURI                                                           | "http://www.example.com/nlcs/def/1e02896a-657e-4b98-8558-746485c91f82" |
|---------------------------------------------------------------------------|-----------------------------------------------------------------------------------|
| "id"                                                                      | "1"                                                                               |
| "sbibliotheek"                                                            | "SAL"                                                                             |
| "omschrijving"                                                            | "Algemeen"                                                                        |


## Tabel status 
De [query voor status](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Statussen)  ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Status information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?statusURI ?id ?status ?omschrijving ?weergave_als
WHERE {
  ?DisciplineTopConcept skos:prefLabel "Statussen" .
  ?statusURI a ?DisciplineTopConcept .
  ?statusURI skos:prefLabel ?status .
  ?statusURI skos:altLabel ?weergave_als .
  ?statusURI skos:definition ?omschrijving .
  OPTIONAL {
    ?statusURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  }
} ORDER BY ?id
</pre>

De query voor status resulteert in een tabel zoals deze:
| statusURI                                                           | "http://www.example.com/nlcs/def/1530e0da-0d59-4846-87fe-6e0462d7e914" |
|---------------------------------------------------------------------|-----------------------------------------------------------------------------------|
| "id"                                                                | "1"                                                                               |
| "status"                                                            | "N"                                                                               |
| "omschrijving"                                                      | "nieuw werk"                                                                      |
| "weergave_als"                                                      | "N"                                                                               |



## Tabel symbolen
De [query voor symbolen](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Symbolen.rq) ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Symbool information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?symboolURI ?id ?sbibliotheek ?sbibliotheekURI ?fase ?symbool ?optie ?fileURL
WHERE {
  ?SymboolTopConcept a owl:Class .
  ?SymboolTopConcept skos:prefLabel "Symbool" .
  ?sbibliotheekURI rdfs:subClassOf ?SymboolTopConcept .
  ?sbibliotheekURI skos:prefLabel ?sbibliotheek .
  ?symboolURI rdfs:subClassOf+ ?sbibliotheekURI .
  ?symboolURI skos:prefLabel ?symbool .
  FILTER (NOT EXISTS{?subSymbool rdfs:subClassOf ?symboolURI}) .  
  OPTIONAL {
    ?symboolURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  }
  OPTIONAL {
    ?symboolURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  }
  OPTIONAL {
    ?symboolURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  } 
  OPTIONAL {
    ?symboolURI ?urlProperty ?fileURL .
    ?urlProperty skos:prefLabel "URL" .
  } 
} ORDER BY ?id
</pre>


De query voor symbolen resulteert in een tabel zoals deze:

| symboolURI | id | sbibliotheek | sbibliotheekURI | fase | symbool | optie | fileURL |
|------------|----|--------------|-----------------|------|---------|-------|---------|
| http://www.example.com/nlcs/def/a4b7a2b0-2cd5-4d84-b93c-ecf62b110aa3 | 1 | SAL | http://www.example.com/nlcs/def/1e02896a-657e-4b98-8558-746485c91f82 |  | SAL-TEKENBLAD_A0-SO | SO |  |


## Wijzigingen tussen versies


De [query voor ...]() ziet er als volgt uit: 

<pre data-codelanguage="sparql">
</pre>

De query resulteert in een tabel zoals deze:


## Tabel bgt mapping
De [query voor ...]() resulteert in een tabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
</pre>

## Tabel GWSW mapping

De [query voor ...]() resulteert in een tabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
</pre>




