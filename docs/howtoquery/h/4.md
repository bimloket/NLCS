# Query's

 
## Tabel abibliotheken

de abibliotheken zijn de "arceringsbibliotheken", oftewel de NLCS hoofdgroepen voor arceringen.

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Arceringen bibliotheek information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?abibliotheekURI ?id ?abibliotheek ?omschrijving
WHERE {
  ?ArceringTopConcept a owl:Class .
  ?ArceringTopConcept skos:prefLabel "Arcering" .
  ?abibliotheekURI rdfs:subClassOf ?ArceringTopConcept .
  ?abibliotheekURI skos:prefLabel ?abibliotheek .
  ?abibliotheekURI skos:definition ?omschrijving .
  OPTIONAL {
    ?abibliotheekURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  } 
} ORDER BY ?id
</pre>

De [query voor abibliotheken](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Abibliotheken.rq) resulteert in een tabel zoals deze:

| abibliotheekURI                                                      | id | abibliotheek | omschrijving |
|--------------------------------------------------------------------------------|----|-----|------------|
| https://www.example.org/nlcs/def/02ffcdec-87e9-4ea3-8aa6-7ae6d32470c4 | 1   | ACO |     Constructies       |
| https://www.example.org/nlcs/def/95d72fe5-9222-417d-a5d6-dcc9b296a9e4 |  2  | AGR |    Groen        |
| https://www.example.org/nlcs/def/00741035-a28a-4e9d-89e1-c581c7fdb8a5 |   3 | AGW |      Grondwerk      |



## Tabel arceringen



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Arcering information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?arceringURI ?id ?abibliotheekURI ?abibliotheek ?fase ?arcering ?optie ?schaal ?vrkl_kort ?vrkl_lang ?fileURL
WHERE {
  ?ArceringTopConcept a owl:Class .
  ?ArceringTopConcept skos:prefLabel "Arcering" .
  ?abibliotheekURI rdfs:subClassOf ?ArceringTopConcept .
  ?abibliotheekURI skos:definition ?omschrijving .
  ?abibliotheekURI skos:prefLabel ?abibliotheek .
  ?arceringURI rdfs:subClassOf+ ?abibliotheekURI .
  ?arceringURI skos:prefLabel ?arcering .
  FILTER( NOT EXISTS { ?subArcering rdfs:subClassOf ?arceringURI . }) .
  OPTIONAL {
    ?arceringURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  } 
  OPTIONAL {
    ?arceringURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  } 
  OPTIONAL {
    ?arceringURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  } 
  OPTIONAL {
    ?arceringURI ?vrkl_kortProperty ?vrkl_kort .
    ?vrkl_kortProperty skos:prefLabel "VRKL_KORT" .
  } 
  OPTIONAL {
    ?arceringURI ?vrkl_langProperty ?vrkl_lang .
    ?vrkl_langProperty skos:prefLabel "VRKL_LANG" .
  }
  OPTIONAL {
    ?arceringURI ?schaalProperty ?schaal .
    ?schaalProperty skos:prefLabel "Schaal" .
  } 
  OPTIONAL {
    ?arceringURI ?urlProperty ?fileURL .
    ?urlProperty skos:prefLabel "URL" .
  } 
} ORDER BY ?id
</pre>

De [query voor arceringen](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Arceringen.rq) resulteert in een tabel zoals deze, gespiegeld voor betere leesbaarheid (de inhoud klopt niet):

|                           | Row 1                                                  | Row 2                                                  | Row 3                                                  | Row 4                                                  |
|---------------------------|--------------------------------------------------------|--------------------------------------------------------|--------------------------------------------------------|--------------------------------------------------------|
| **arceringURI**                 |  https://www.example.org/nlcs/def/55c1f87b-535c-4532-ace8-9e62694488db | hhttps://www.example.org/nlcs/def/37d2c991-aebd-4df0-8c12-8361d4df6cd8 | hhttps://www.example.org/nlcs/def/338b9a47-db06-4856-91df-01ef09f3d7e51 | hhttps://www.example.org/nlcs/def/37868743-baf2-4572-a427-9f460de5725a |  
| **id**                    | 19                                                     | 20                                                     | 18                                                     | 12                                                     |
| abibliotheekURI                 |  https://www.example.org/nlcs/def/55c1f87b-535c-4532-ace8-9e62694488db1 | hhttps://www.example.org/nlcs/def/37d2c991-aebd-4df0-8c12-8361d4df6cd81 | hhttps://www.example.org/nlcs/def/338b9a47-db06-4856-91df-01ef09f3d7e5 | hhttps://www.example.org/nlcs/def/37868743-baf2-4572-a427-9f460de5725a |  
| **abibliotheek**                   | ACO                                                    | ACO                                                    | ACO                                                    | ACO                                                    |
| **fase**              |                B                                       |                                                        |                                                        |                                                        |
| **arcering**              | ACO-HOUT_02-SODMM                                      | ACO-HOUT_03-SODMM                                      | ACO-HOUT_01-SODMM                                      | ACO-BETON_VOORGESP_PREFAB-SODMM                        |
| **optie**                   | SODMM                                                  | SODMM                                                  | SODMM                                                  | SODMM                                                  |
| **schaal**              |       1                                                |              1                                          |             1                                           |                                                        |
| **vrkl_kort**             | hout02                                                 | hout03                                                 | hout01                                                 | prefabbeton                                            |
| **vrkl_lang**             | 45,0,0,0,0.2                                           | 0,0,0,0,0.3                                            | 45,0,0,0,1.875 135,0,0,3.75,1.875,4.6875,-2.8125       | 45,0,0,0,2.5 -45,0,0,0,2.5 0,0,0,0,.75,0,-0.75        |
| **fileURL**                |  https://www.example.org/nlcs/def/55c1f87b-535c-4532-ace8-9e62694488db2 | hhttps://www.example.org/nlcs/def/37d2c991-aebd-4df0-8c12-8361d4df6cd82 | hhttps://www.example.org/nlcs/def/338b9a47-db06-4856-91df-01ef09f3d7e52 | hhttps://www.example.org/nlcs/def/37868743-baf2-4572-a427-9f460de5725a |  

## Tabel disciplines

De query voor disciplines ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Discipline information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?disciplineURI ?id ?code ?verklaring ?omschrijving
WHERE {
  ?DisciplineTopConcept skos:prefLabel "Disciplines" .
  ?disciplineURI a ?DisciplineTopConcept .
  ?disciplineURI skos:prefLabel ?code .
  ?disciplineURI skos:altLabel ?verklaring .
  ?disciplineURI skos:definition ?omschrijving .
  OPTIONAL {
    ?disciplineURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  }
} ORDER BY ?id
</pre>

De [query voor disciplines](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Disciplines.rq) resulteert in een tabel zoals deze:

| disciplineURI|  id   |   code | verklaring |  omschrijving
|------------|------|-------------|
| http://www.example.org/nlcs/def/4adb2e90-c529-450e-ad37-fe254e218e90 |   8   |   CO | Vorm- en wapeningstekeningen van civieltechnische constructies, zoals: brug, tunnel, onderdoorgang, viaduct, sluizencomplex, spoortunnel, faunapassage. |



## Hoofdgroepen


De query voor hoofdgroepen ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Hoofdgroep information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?hoofdgroepURI ?id ?hoofdgroep ?afkorting
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  ?hoofdgroepURI rdfs:subClassOf ?NLCSObjectTopConcept .
  ?hoofdgroepURI skos:prefLabel ?afkorting .
  ?hoofdgroepURI skos:definition ?hoofdgroep .
  OPTIONAL {
    ?hoofdgroepURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  } 
} ORDER BY ?id
</pre>

De [query voor hoofdgroepen](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Hoofdgroepen.rq) resulteert in een tabel zoals deze:

| hoofdgroepURI                                                       | id | hoofdgroep                  | afkorting |
|---------------------------------------------------------------------|----|-----------------------------|-----------|
| http://www.example.com/nlcs/def/5b0d986a-88d3-4bb4-bc6f-fe940836c242 | 1  | Voor alle hoofdgroepen      | ZZ        |
| http://www.example.com/nlcs/def/6c4b8ac2-7a72-4ae6-b169-4dec77be3f02 | 10 | Kadastrale informatie en Grenzen | KG        |
| http://www.example.com/nlcs/def/cf3d9f17-91ab-409b-be8b-25391d045637 | 11 | Kabels en Leidingen         | KL       




## Objectentabellen per hoofdgroep

De [query objectentabellen per hoofdgroep](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Objects.rq) ziet er als volgt uit, merk op dat de database zo groot is, dat een query van alle objecten in alle hoofdgroepen niet mogelijk is, je kunt deze informatie alleen per hoofdgroep opvragen. In deze query komen ook de URI's en de relaties met arceringen en symbolen terug, waardoor er extra rijen ontstaan als je dit in CSV formaat opvraagt. Daarom is er ook een verkorte query, zonder die onderdelen.

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Object information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?objectURI ?hoofdgroepURI ?hoofdgroep ?id_nummer ?omschrijving ?kind_vanURI ?kind_van ?status ?discipline
?lw_bURI ?lw_b ?kl_bURI ?kl_b ?kl_b_aURI ?kl_b_a ?kl_b_gdURI ?kl_b_gd ?kl_b_gnURI ?kl_b_gn ?kl_b_vURI ?kl_b_v ?lt_bURI ?lt_b
?lw_nURI ?lw_n ?kl_nURI ?kl_n ?kl_n_aURI ?kl_n_a ?kl_n_gdURI ?kl_n_gd ?kl_n_gnURI ?kl_n_gn ?kl_n_vURI ?kl_n_v ?lt_nURI ?lt_n
?lw_tURI ?lw_t ?kl_tURI ?kl_t ?kl_t_aURI ?kl_t_a ?kl_t_gdURI ?kl_t_gd ?kl_t_gnURI ?kl_t_gn ?kl_t_vURI ?kl_t_v ?lt_tURI ?lt_t
?lw_vURI ?lw_v ?kl_vURI ?kl_v ?kl_v_aURI ?kl_v_a ?kl_v_gdURI ?kl_v_gd ?kl_v_gnURI ?kl_v_gn ?kl_v_vURI ?kl_v_v ?lt_vURI ?lt_v
?element ?sobjectURI ?sobject ?aobjectURI ?aobject ?vrkl_kort
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  ?hoofdgroepURI rdfs:subClassOf ?NLCSObjectTopConcept .
  ?hoofdgroepURI skos:prefLabel ?hoofdgroep .
  ?objectURI rdfs:subClassOf+ ?hoofdgroepURI .
  ?objectURI skos:prefLabel ?omschrijving .
  FILTER (?hoofdgroep = "AL") .  # INPUT REQUIRED - Change value to the desired hoofdgroep, AL is used as example
  OPTIONAL {
    ?objectURI ?idProperty ?id_nummer .
    ?idProperty skos:prefLabel "ID" .
  } 
  OPTIONAL {
    ?objectURI rdfs:subClassOf ?kind_vanURI .
    FILTER (?kind_vanURI != ?hoofdgroepURI) .
    ?kind_vanURI ?idProperty ?kind_van .
    ?idProperty skos:prefLabel "ID" .
  }
  OPTIONAL {
    ?objectURI ?elementProperty ?element .
    ?elementProperty skos:prefLabel "Element" .
  } 
  OPTIONAL {
    ?objectURI ?vrkl_kortProperty ?vrkl_kort .
    ?vrkl_kortProperty skos:prefLabel "VRKL_KORT" .
  } 

  ?objectURI rdfs:subClassOf ?bnodeStatus .
  ?bnodeStatus owl:onProperty ?StatusProperty .
  ?StatusProperty skos:prefLabel "Status" .
  OPTIONAL {
    ?bnodeStatus owl:hasValue/skos:prefLabel ?StatusValue .
  }
  BIND( COALESCE(?StatusValue, "*") AS ?status) .
  ?objectURI rdfs:subClassOf ?bnodeDiscipline .
  ?bnodeDiscipline owl:onProperty ?DisciplineProperty .
  ?DisciplineProperty skos:prefLabel "Discipline" .
  OPTIONAL {
    ?bnodeDiscipline owl:hasValue/skos:prefLabel ?DisciplineValue .
  }
  BIND( COALESCE(?DisciplineValue, "**") AS ?discipline) .

  OPTIONAL {
    ?objectURI rdfs:subClassOf ?bnodeSymbool .
    ?bnodeSymbool owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
    ?bnodeSymbool owl:onClass ?sobjectURI .
    ?sobjectURI rdfs:subClassOf* ?SymboolTopConcept .
    ?SymboolTopConcept skos:prefLabel "Symbool" .
    ?sobjectURI skos:prefLabel ?sobject .
  }
  OPTIONAL {
    ?objectURI rdfs:subClassOf ?bnodeArcering .
    ?bnodeArcering owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
    ?bnodeArcering owl:onClass ?aobjectURI .
    ?aobjectURI rdfs:subClassOf* ?SymboolTopConcept .
    ?ArceringTopConcept skos:prefLabel "Arcering" .
    ?aobjectURI skos:prefLabel ?aobject .
  }
  OPTIONAL {
    ?objectURI ?elementProperty ?element .
    ?elementProperty skos:prefLabel "VRKL_KORT" .
  } 

  OPTIONAL {
    ?objectURI ?BLineweightProperty ?lw_bURI .
    ?BLineweightProperty skos:prefLabel "B lineweight" .
    ?lw_bURI skos:prefLabel ?lw_b .

    ?objectURI ?BColorProperty ?kl_bURI .
    ?BColorProperty skos:prefLabel "B color" .
    ?kl_bURI skos:prefLabel ?kl_b .

    ?objectURI ?BColorAProperty ?kl_b_aURI .
    ?BColorAProperty skos:prefLabel "B color A" .
    ?kl_b_aURI skos:prefLabel ?kl_b_a .

    ?objectURI ?BColorGDProperty ?kl_b_gdURI .
    ?BColorGDProperty skos:prefLabel "B color GD" .
    ?kl_b_gdURI skos:prefLabel ?kl_b_gd .

    ?objectURI ?BColorGNProperty ?kl_b_gnURI .
    ?BColorGNProperty skos:prefLabel "B color GN" .
    ?kl_b_gnURI skos:prefLabel ?kl_b_gn .

    ?objectURI ?BColorVProperty ?kl_b_vURI .
    ?BColorVProperty skos:prefLabel "B color V" .
    ?kl_b_vURI skos:prefLabel ?kl_b_v .

    ?objectURI ?BLinetypeProperty ?lt_bURI .
    ?BLinetypeProperty skos:prefLabel "B linetype" .
    ?lt_bURI skos:prefLabel ?lt_b .
  } 
  OPTIONAL {
    ?objectURI ?NLineweightProperty ?lw_nURI .
    ?NLineweightProperty skos:prefLabel "N lineweight" .
    ?lw_nURI skos:prefLabel ?lw_n .

    ?objectURI ?NColorProperty ?kl_nURI .
    ?NColorProperty skos:prefLabel "N color" .
    ?kl_nURI skos:prefLabel ?kl_n .

    ?objectURI ?NColorAProperty ?kl_n_aURI .
    ?NColorAProperty skos:prefLabel "N color A" .
    ?kl_n_aURI skos:prefLabel ?kl_n_a .

    ?objectURI ?NColorGDProperty ?kl_n_gdURI .
    ?NColorGDProperty skos:prefLabel "N color GD" .
    ?kl_n_gdURI skos:prefLabel ?kl_n_gd .

    ?objectURI ?NColorGNProperty ?kl_n_gnURI .
    ?NColorGNProperty skos:prefLabel "N color GN" .
    ?kl_n_gnURI skos:prefLabel ?kl_n_gn .

    ?objectURI ?NColorVProperty ?kl_n_vURI .
    ?NColorVProperty skos:prefLabel "N color V" .
    ?kl_n_vURI skos:prefLabel ?kl_n_v .

    ?objectURI ?NLinetypeProperty ?lt_nURI .
    ?NLinetypeProperty skos:prefLabel "N linetype" .
    ?lt_nURI skos:prefLabel ?lt_n .
  } 
  OPTIONAL {
    ?objectURI ?TLineweightProperty ?lw_tURI .
    ?TLineweightProperty skos:prefLabel "T lineweight" .
    ?lw_tURI skos:prefLabel ?lw_t .

    ?objectURI ?TColorProperty ?kl_tURI .
    ?TColorProperty skos:prefLabel "T color" .
    ?kl_tURI skos:prefLabel ?kl_t .

    ?objectURI ?TColorAProperty ?kl_t_aURI .
    ?TColorAProperty skos:prefLabel "T color A" .
    ?kl_t_aURI skos:prefLabel ?kl_t_a .

    ?objectURI ?TColorGDProperty ?kl_t_gdURI .
    ?TColorGDProperty skos:prefLabel "T color GD" .
    ?kl_t_gdURI skos:prefLabel ?kl_t_gd .

    ?objectURI ?TColorGNProperty ?kl_t_gnURI .
    ?TColorGNProperty skos:prefLabel "T color GN" .
    ?kl_t_gnURI skos:prefLabel ?kl_t_gn .

    ?objectURI ?TColorVProperty ?kl_t_vURI .
    ?TColorVProperty skos:prefLabel "T color V" .
    ?kl_t_vURI skos:prefLabel ?kl_t_v .

    ?objectURI ?TLinetypeProperty ?lt_tURI .
    ?TLinetypeProperty skos:prefLabel "T linetype" .
    ?lt_tURI skos:prefLabel ?lt_t .
  } 
  OPTIONAL {
    ?objectURI ?VLineweightProperty ?lw_vURI .
    ?VLineweightProperty skos:prefLabel "V lineweight" .
    ?lw_vURI skos:prefLabel ?lw_v .

    ?objectURI ?VColorProperty ?kl_vURI .
    ?VColorProperty skos:prefLabel "V color" .
    ?kl_vURI skos:prefLabel ?kl_v .

    ?objectURI ?VColorAProperty ?kl_v_aURI .
    ?VColorAProperty skos:prefLabel "V color A" .
    ?kl_v_aURI skos:prefLabel ?kl_v_a .

    ?objectURI ?VColorGDProperty ?kl_v_gdURI .
    ?VColorGDProperty skos:prefLabel "V color GD" .
    ?kl_v_gdURI skos:prefLabel ?kl_v_gd .

    ?objectURI ?VColorGNProperty ?kl_v_gnURI .
    ?VColorGNProperty skos:prefLabel "V color GN" .
    ?kl_v_gnURI skos:prefLabel ?kl_v_gn .

    ?objectURI ?VColorVProperty ?kl_v_vURI .
    ?VColorVProperty skos:prefLabel "V color V" .
    ?kl_v_vURI skos:prefLabel ?kl_v_v .

    ?objectURI ?VLinetypeProperty ?lt_vURI .
    ?VLinetypeProperty skos:prefLabel "V linetype" .
    ?lt_vURI skos:prefLabel ?lt_v .
  } 
  
} ORDER BY ?id
</pre>


De [query voor de tabellen van de hoofdgroepen](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20hoofdgroepen.rq) resulteert in een tabel zoals deze, de rijen en kolommen zijn omgekeerd ten behoeve van de leesbaarheid in dit document:

| Kenmerk      | Inhoud                                                                                                                             |
|--------------|-----------------------------------------------------------------------------------------------------------------------------------|
| objectURI    | [http://www.example.com/nlcs/def/cf01cca0-27f9-40d1-8c1f-bc45a5f2bc18](http://www.example.com/nlcs/def/cf01cca0-27f9-40d1-8c1f-bc45a5f2bc18) |
| hoofdgroepURI  | [http://www.example.com/nlcs/def/0a74f373-7382-405d-99ec-50e44e6483fc](http://www.example.com/nlcs/def/0a74f373-7382-405d-99ec-50e44e6483fc) |
| hoofdgroep   | RI                                                                                                                              |
| id_nummer    | 3487                                                                                                                            |
| omschrijving   | GELEIDECONSTRUCTIE_GELEIDERAIL_KOPPELING_SCHILDMUUR                                                                                  |
| kind_vanURI   | [http://www.example.com/nlcs/def/e6fd30ed-ed03-4863-86ca-12f7abbbc53a](http://www.example.com/nlcs/def/e6fd30ed-ed03-4863-86ca-12f7abbbc53a) |
| kind_van      | 220                                                                                                                               |
| status          | *                                                                                                                                |
| discipline    | **                                                                                                                                  |
| lw_bURI       | [http://www.example.com/nlcs/def/bacc787a-3288-4f27-a233-6a58ce3d5c65](http://www.example.com/nlcs/def/bacc787a-3288-4f27-a233-6a58ce3d5c65) |
| lw_b              | 0.18                                                                                                                             |
| kl_bURI       | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_b            | 20                                                                                                                               |
| kl_b_aURI   | [http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec](http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec) |
| kl_b_a           | 22                                                                                                                              |
| kl_b_gdURI  | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_b_gd        | 20                                                                                                                                |
| kl_b_gnURI  | [http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3](http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3) |
| kl_b_gn        | 253                                                                                                                              |
| kl_b_vURI    | [http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437](http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437) |
| kl_b_v           | CONTINUOUS                                                                                                                    |
| lt_bURI        | [http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4](http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4) |
| lw_nURI       | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| lw_n            | 20                                                                                                                               |
| kl_nURI        | [http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec](http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec) |
| kl_n             | 22                                                                                                                               |
| kl_n_aURI     | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_n_a           | 20                                                                                                                               |
| kl_n_gdURI   | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_n_gd        | 20                                                                                                                               |
| kl_n_gnURI   | [http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3](http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3) |
| kl_n_gn         | 253                                                                                                                               |
| kl_n_vURI     | [http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437](http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437) |
| kl_n_v            | CONTINUOUS                                                                                                                     |
| lt_nURI        | [http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4](http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4) |
| lw_tURI        | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| lw_t             | 20                                                                                                                               |
| kl_tURI         | [http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec](http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec) |
| kl_t              | 22                                                                                                                               |
| kl_t_aURI      | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_t_a             | 20                                                                                                                               |
| kl_t_gdURI    | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_t_gd            | 20                                                                                                                               |
| kl_t_gnURI   | [http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3](http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3) |
| kl_t_gn           | 253                                                                                                                               |
| kl_t_vURI       | [http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437](http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437) |
| kl_t_v                | CONTINUOUS                                                                                                                    |
| lt_tURI        | [http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4](http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4) |
| lw_vURI       | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| lw_v            | 20                                                                                                                                  |
| kl_vURI        | [http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec](http://www.example.com/nlcs/def/c4aec6de-1216-4b16-8b17-fbae071d95ec) |
| kl_v             | 22                                                                                                                                 |
| kl_v_aURI     | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_v_a          | 20                                                                                                                               |
| kl_v_gdURI   | [http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593](http://www.example.com/nlcs/def/0a66d54d-0be9-40e8-a9c3-4d450ba8e593) |
| kl_v_gd           | 20                                                                                                                               |
| kl_v_gnURI   | [http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3](http://www.example.com/nlcs/def/50f7e6bd-88c4-4cb6-a60b-64408590c6a3) |
| kl_v_gn           | 253                                                                                                                               |
| kl_v_vURI        | [http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437](http://www.example.com/nlcs/def/21484fcb-4d9f-4e86-a9f5-fd6d855c7437) |
| kl_v_v               | CONTINUOUS                                                                                                                     |
| lt_vURI          | [http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4](http://www.example.com/nlcs/def/f14aa137-98dc-41ad-8dc5-fad94cb489e4) |
| element           | G/S                                                                                                                               |
| sobjectURI       | [http://www.example.com/nlcs/def/6d076150-6701-4fec-bff9-a8489ce32bd4](http://www.example.com/nlcs/def/6d076150-6701-4fec-bff9-a8489ce32bd4) |
| sobject           | SRI-SCHILDMUUR                                                                                                                    |
| aobjectURI       | [http://www.example.com/nlcs/def/6d076150-6701-4fec-bff9-a8489ce32bd4](http://www.example.com/nlcs/def/6d076150-6701-4fec-bff9-a8489ce32bd4) |
| aobject           | SRI-SCHILDMUUR                                                                                                                     |
| vrkl_kort         | ""                                                                                                                                    |

## Verkorte objectentabellen per hoofdgroep
De [Verkortequeryobjectentabel](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Objects_verkort) is bedoeld voor het genereren van leesbare objectentabellen ten behoeve van de inhoudelijk experts die aan de standaard werken. Deze bevatten geen relaties met symbolen en arceringen, zodat er geen dubbele regels per object ontstaan. En geen URI's, omdat die niet voor menselijke lezers bedoeld zijn.



## Tabel lijnkleuren
De [query voor lijnkleuren](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Lijnkleuren.rq) ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Lijnkleur information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?lijnkleurURI ?id ?fase ?nummer ?hex ?R ?G ?B ?optie
WHERE {
  ?LijnkleurTopConcept a owl:Class .
  ?LijnkleurTopConcept skos:prefLabel "Lijnkleur" . 
  ?lijnkleurURI rdfs:subClassOf ?LijnkleurTopConcept .
  ?lijnkleurURI skos:prefLabel ?nummer .
  OPTIONAL {
  	?lijnkleurURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  }
  OPTIONAL {
  	?lijnkleurURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  }
  OPTIONAL {
  	?lijnkleurURI ?hexProperty ?hex .
    ?hexProperty skos:prefLabel "HEX" .
  }
  OPTIONAL {
  	?lijnkleurURI ?RProperty ?R .
    ?RProperty skos:prefLabel "R" .
  }
  OPTIONAL {
  	?lijnkleurURI ?GProperty ?G .
    ?GProperty skos:prefLabel "G" .
  }
  OPTIONAL {
  	?lijnkleurURI ?BProperty ?B .
    ?BProperty skos:prefLabel "B" .
  }
  OPTIONAL {
  	?lijnkleurURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  }
} ORDER BY ?id
</pre>

De query voor lijnkleuren resulteert in een tabel zoals deze:

| lijnkleurURI                                                       | id  | fase | nummer | hex     | R   | G   | B   | optie |
|--------------------------------------------------------------------|-----|------|--------|---------|-----|-----|-----|-------|
| http://www.example.com/nlcs/def/40a13afa-d228-461a-981f-87e23784a038 | 100 | 0    | 0      | #000000 | 0   | 0   | 0   |       |
| http://www.example.com/nlcs/def/89970413-24e9-4574-a5df-d6a572d13fac | 101 | 0    | 1      | #FF0000 | 255 | 0   | 0   |       |
| http://www.example.com/nlcs/def/af48781d-0a36-4244-88ff-cb86c15b5313 | 102 | 0    | 2      | #FFFF00 | 255 | 255 | 0   |       |


## Tabel lijntypes


De [query voor lijntypes](https://github.com/nl-digigo/NLCS/blob/main/code/NLCS_Query_Lijntypes.rq) ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Lijntype information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?lijntypeURI ?id ?hoofdgroep ?omschrijving ?fase ?optie ?autocaddef ?fileURL
WHERE {
  ?LijntypeTopConcept a owl:Class .
  ?LijntypeTopConcept skos:prefLabel "Lijntype" .
  ?Hoofdgroep rdfs:subClassOf ?LijntypeTopConcept .
  ?Hoofdgroep skos:prefLabel ?HoofdgroepName .
  ?lijntypeURI rdfs:subClassOf* ?Hoofdgroep .
  ?lijntypeURI skos:prefLabel ?omschrijving .
  BIND ( IF(?lijntypeURI = ?Hoofdgroep, IF(?omschrijving IN ("V-CONTINUOUS-SO", "CONTINUOUS", "NLCS_HIDDEN-SO"), ?lijntypeURI, ?undefined), ?lijntypeURI) AS ?NotHoofdgroep) .
  BIND ( IF(?lijntypeURI = ?Hoofdgroep, IF(?omschrijving IN ("V-CONTINUOUS-SO", "CONTINUOUS", "NLCS_HIDDEN-SO"), ?undefined, ?HoofdgroepName), ?HoofdgroepName) AS ?hoofdgroep) .
  FILTER (?lijntypeURI = ?NotHoofdgroep) .
  OPTIONAL {
    ?lijntypeURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  } OPTIONAL {
    ?lijntypeURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  } OPTIONAL {
    ?lijntypeURI skos:definition ?autocaddef .
  } OPTIONAL {
    ?lijntypeURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  }
  OPTIONAL {
    ?lijntypeURI ?urlProperty ?fileURL .
    ?urlProperty skos:prefLabel "URL" .
  } 
} ORDER BY ?id
</pre>

De query voor lijntypes resulteert in een tabel zoals deze, gespiegeld voor betere leesbaarheid in ReSpec:

| Kenmerk         | Inhoud                                                                                                                         |
|----------------|---------------------------------------------------------------------------------------------------------------------------------|
| lijntypeURI     | [http://www.example.com/nlcs/def/e6abd6a9-f196-4cb6-8a31-21176939ff74](http://www.example.com/nlcs/def/e6abd6a9-f196-4cb6-8a31-21176939ff74) |
| id              | 116                                                                                                                             |
| hoofdgroep      | KG                                                                                                                               |
| omschrijving    | V-KG-KADASTRAAL-SO                                                                                                               |
| fase            | V                                                                                                                                 |
| optie           | SO                                                                                                                                 |
| autocaddef       | A,5.5,[VERWIJDEREN2,NLCS.SHX,x=-2.75,s=0.6],-2,[K,standard,U=0.0,y=-.9,s=1.8],-2,5.5,[VERWIJDEREN2,NLCS.SHX,x=-2.75,s=0.6],-2,[KRUISJE,NLCS.SHX,s=0.75],-2  |
| fileURL          | ""                                                                                                                                  |




## Tabel lijnweights
De [query voor lijndiktes](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20lineweights.rq) resulteert in een tabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
select distinct ?Lineweight
where { 
	   {
    ?NLCSObject ?BLineweightProperty ?Lineweight .
    ?BLineweightProperty skos:prefLabel "B lineweight" .
  } 
    union {
    ?NLCSObject ?NLineweightProperty ?Lineweight .
    ?NLineweightProperty skos:prefLabel "N lineweight" .
  } 
    union {
    ?NLCSObject ?TLineweightProperty ?Lineweight .
    ?TLineweightProperty skos:prefLabel "T lineweight" .
  } 
    union {
    ?NLCSObject ?VLineweightProperty ?Lineweight .
    ?VLineweightProperty skos:prefLabel "V lineweight" .
  }
} 
orderby ?Lineweight
</pre>

<div class="issue" data-number="273"></div>

## Tabel object
De [query voor een object](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20NLCS-objects.rq) resulteert in een tabel zoals deze, gespiegeld voor leesbaarheid:

De toelichting op de onderdelen van deze tabel staan in de [Code-documentatie](https://nl-digigo.github.io/NLCS/code_documentation/#objecten)

| NLCSObject                                      | https://www.example.org/nlcs/def/c132cd12-d85e-46d8-b862-27c6d713a883 | https://www.example.org/nlcs/def/e1303c6d-5701-4ffb-a68f-1f9b42aa049c | https://www.example.org/nlcs/def/1e16b5eb-015c-4c1f-9d10-6ba71dfa167b |
| ------------------------------------------------ | -------------------------------------------------------------------- | -------------------------------------------------------------------- | -------------------------------------------------------------------- |
| OMSCHRIJVING                   | HOUTVORM                      | AANGRENZENDE CONSTRUCTIE       | VERBINDING                    |
| STATUS | *      | *      | *      |
| DISCIPLINE | **         | **         | **         |
| HOOFDGROEP | HC         | HC         | HC         |
| ARCERING |          |          |          |
| ARCERINGName | *-**-HC-HOUTVORM-G/A | *-**-HC-AANGRENZENDE CONSTRUCTIE-G | *-**-HC-VERBINDING-G |
| SYMBOOL | 0.18    | 0.18    | 0.18    |
| SYMBOOLName | 30           | 7            | 140          |
| LAAGNAAM | 252      | 253      | 252      |
| BLineweight | 40           | 7            | 130          |
| BColor | 10     | 20     | 10     |
| BColorA | 253     | 253     | 253     |
| BColorGD | 40       | 7        | 130      |
| BColorGN | 10      | 20      | 10      |
| BColorV | 253     | 253     | 253     |
| BLinetype | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| BLinetypeName | 0.25         | 0.18         | 0.18         |
| NLineweight | 30     | 7      | 140    |
| NColor | 252     | 253    | 252    |
| NColorA | 40       | 7        | 130      |
| NColorGD | 10      | 20      | 10      |
| NColorGN | 253     | 253     | 253     |
| NColorV | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| NLinetype | 0.25         | 0.18         | 0.18         |
| NLinetypeName | 30     | 7      | 140    |
| TLineweight | 252     | 253    | 252    |
| TColor | 40       | 10      | 130      |
| TColorA | 10      | 20      | 10      |
| TColorGD | 253     | 253     | 253     |
| TColorGN | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| TColorV | 0.25         | 0.18         | 0.18         |
| TLinetype | 30     | 7      | 140    |
| TLinetypeName | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| VLineweight | 0.25         | 0.18         | 0.18         |
| VColor | 30     | 7      | 140    |
| VColorA | 252     | 253    | 252    |
| VColorGD | 40       | 7        | 130      |
| VColorGN | 10      | 20      | 10      |
| VColorV | 253     | 253     | 253     |
| VLinetype | CONTINUOUS | CONTINUOUS | CONTINUOUS |
| VLinetypeName | 0.25         | 0.18         | 0.18         |
| VRKL_KORT | G/A | G | G |
| VRKL_LANG | HOUT | AANGRENZENDE CONSTRUCTIE | VERBINDING |
| ID | 2797      | 2803      | 2800      |
| KIND_VAN |           |           |           |
| FASE |           |           |           |
| Bewerking |           |           |           |
| Element |           |           |           |



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all NLCS-Object information and stores it in the familiar Excel/CSV format

SELECT ?NLCSObject ?OMSCHRIJVING ?STATUS ?DISCIPLINE ?HOOFDGROEP ?ARCERING ?ARCERINGName ?SYMBOOL ?SYMBOOLName ?LAAGNAAM 
?BLineweight ?BColor ?BColorA ?BColorGD ?BColorGN ?BColorV ?BLinetype ?BLinetypeName
?NLineweight ?NColor ?NColorA ?NColorGD ?NColorGN ?NColorV ?NLinetype ?NLinetypeName
?TLineweight ?TColor ?TColorA ?TColorGD ?TColorGN ?TColorV ?TLinetype ?TLinetypeName
?VLineweight ?VColor ?VColorA ?VColorGD ?VColorGN ?VColorV ?VLinetype ?VLinetypeName
?VRKL_KORT ?VRKL_LANG ?ID ?KIND_VAN ?FASE ?Bewerking ?Element
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  ?Hoofdgroep rdfs:subClassOf ?NLCSObjectTopConcept .
  ?Hoofdgroep skos:prefLabel ?HOOFDGROEP .
  ?NLCSObject rdfs:subClassOf+ ?Hoofdgroep .
  ?NLCSObject skos:prefLabel ?OMSCHRIJVING .
  
  ?NLCSObject rdfs:subClassOf ?bnodeStatus .
  ?bnodeStatus owl:onProperty ?StatusProperty .
  ?StatusProperty skos:prefLabel "Status" .
  OPTIONAL {
    ?StatusProperty owl:hasValue/skos:prefLabel ?StatusValue .
  }
  BIND( COALESCE(?StatusValue, "*") AS ?STATUS) .
  ?NLCSObject rdfs:subClassOf ?bnodeDiscipline .
  ?bnodeDiscipline owl:onProperty ?DisciplineProperty .
  ?DisciplineProperty skos:prefLabel "Discipline" .
  OPTIONAL {
    ?DisciplineProperty owl:hasValue/skos:prefLabel ?DisciplineValue .
  }
  BIND( COALESCE(?DisciplineValue, "**") AS ?DISCIPLINE) .
  OPTIONAL {
    ?NLCSObject rdfs:subClassOf ?bnodeArcering .
    ?bnodeArcering owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
    ?bnodeArcering owl:onClass ?ARCERING .
    ?ARCERING rdfs:subClassOf* ?ArceringTopConcept .
    ?ArceringTopConcept skos:prefLabel "Arcering" .
    ?ARCERING skos:prefLabel ?ARCERINGName .
    FILTER( CONTAINS(STR(?ARCERING), "https://www.example.org/nlcs/def/"))
  } OPTIONAL {
    ?NLCSObject rdfs:subClassOf ?bnodeSymbool .
    ?bnodeSymbool owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
    ?bnodeSymbool owl:onClass ?SYMBOOL .
    ?SYMBOOL rdfs:subClassOf* ?SymboolTopConcept .
    ?SymboolTopConcept skos:prefLabel "Symbool" .
    ?SYMBOOL skos:prefLabel ?SYMBOOLName .
    FILTER( CONTAINS(STR(?SYMBOOL), "https://www.example.org/nlcs/def/"))
  }
    OPTIONAL {    BIND (STRBEFORE(STR(?SYMBOOL), "-") AS ?FASE ) . }
    ?NLCSObject ?LaagnaamProperty ?LAAGNAAM .
    ?LaagnaamProperty skos:prefLabel "Laagnaam" .
  OPTIONAL {
    ?NLCSObject ?BLineweightProperty ?BLineweight .
    ?BLineweightProperty skos:prefLabel "B lineweight" .
    ?NLCSObject ?BColorProperty ?BColor .
    ?BColorProperty skos:prefLabel "B color" .
    ?NLCSObject ?BColorAProperty ?BColorA .
    ?BColorAProperty skos:prefLabel "B color A" .
    ?NLCSObject ?BColorGDProperty ?BColorGD .
    ?BColorGDProperty skos:prefLabel "B color GD" .
    ?NLCSObject ?BColorGNProperty ?BColorGN .
    ?BColorGNProperty skos:prefLabel "B color GN" .
    ?NLCSObject ?BColorVProperty ?BColorV .
    ?BColorVProperty skos:prefLabel "B color V" .
    ?NLCSObject ?BLinetypeProperty ?BLinetype .
    ?BLinetypeProperty skos:prefLabel "B linetype" .
    ?BLinetype skos:prefLabel ?BLinetypeName .
  } OPTIONAL {
    ?NLCSObject ?NLineweightProperty ?NLineweight .
    ?NLineweightProperty skos:prefLabel "N lineweight" .
    ?NLCSObject ?NColorProperty ?NColor .
    ?NColorProperty skos:prefLabel "N color" .
    ?NLCSObject ?NColorAProperty ?NColorA .
    ?NColorAProperty skos:prefLabel "N color A" .
    ?NLCSObject ?NColorGDProperty ?NColorGD .
    ?NColorGDProperty skos:prefLabel "N color GD" .
    ?NLCSObject ?NColorGNProperty ?NColorGN .
    ?NColorGNProperty skos:prefLabel "N color GN" .
    ?NLCSObject ?NColorVProperty ?NColorV .
    ?NColorVProperty skos:prefLabel "N color V" .
    ?NLCSObject ?NLinetypeProperty ?NLinetype .
    ?NLinetypeProperty skos:prefLabel "N linetype" .
    ?NLinetype skos:prefLabel ?NLinetypeName .
  } OPTIONAL {
    ?NLCSObject ?TLineweightProperty ?TLineweight .
    ?TLineweightProperty skos:prefLabel "T lineweight" .
    ?NLCSObject ?TColorProperty ?TColor .
    ?TColorProperty skos:prefLabel "T color" .
    ?NLCSObject ?TColorAProperty ?TColorA .
    ?TColorAProperty skos:prefLabel "T color A" .
    ?NLCSObject ?TColorGDProperty ?TColorGD .
    ?TColorGDProperty skos:prefLabel "T color GD" .
    ?NLCSObject ?TColorGNProperty ?TColorGN .
    ?TColorGNProperty skos:prefLabel "T color GN" .
    ?NLCSObject ?TColorVProperty ?TColorV .
    ?TColorVProperty skos:prefLabel "T color V" .
    ?NLCSObject ?TLinetypeProperty ?TLinetype .
    ?TLinetypeProperty skos:prefLabel "T linetype" .
    ?TLinetype skos:prefLabel ?TLinetypeName .
  } OPTIONAL {
    ?NLCSObject ?VLineweightProperty ?VLineweight .
    ?VLineweightProperty skos:prefLabel "V lineweight" .
    ?NLCSObject ?VColorProperty ?VColor .
    ?VColorProperty skos:prefLabel "V color" .
    ?NLCSObject ?VColorAProperty ?VColorA .
    ?VColorAProperty skos:prefLabel "V color A" .
    ?NLCSObject ?VColorGDProperty ?VColorGD .
    ?VColorGDProperty skos:prefLabel "V color GD" .
    ?NLCSObject ?VColorGNProperty ?VColorGN .
    ?VColorGNProperty skos:prefLabel "V color GN" .
    ?NLCSObject ?VColorVProperty ?VColorV .
    ?VColorVProperty skos:prefLabel "V color V" .
    ?NLCSObject ?VLinetypeProperty ?VLinetype .
    ?VLinetypeProperty skos:prefLabel "V linetype" .
    ?VLinetype skos:prefLabel ?VLinetypeName .
  }
  OPTIONAL {
    ?NLCSObject ?VRKLProperty ?VRKL_KORT .
    ?VRKLProperty skos:prefLabel "VRKL_KORT" .
  } OPTIONAL {
    ?NLCSObject ?IDProperty ?ID .
    ?IDProperty skos:prefLabel "ID" .
  } OPTIONAL {
    ?NLCSObject rdfs:subClassOf ?NLCSParentObject .
    ?NLCSParentObject ?IDPropertyParent ?KIND_VAN .
    ?IDPropertyParent skos:prefLabel "ID" .
  } 
    OPTIONAL {
    ?NLCSObject ?VRKL_LANGProperty ?VRKL_LANG .
    ?VRKL_LANGProperty skos:prefLabel "VRKL_LANG" .
  } 
    bind (strafter(strafter (strafter (?Laagnaam,"-"),"-"),"-") as ?ObjectTotal )
    bind (strafter (strafter (strafter (?Laagnaam,"-"),"-"),"-") as ?ElementPrePre )
    bind ( if ( contains(?ElementPrePre, "-"), strafter(?ElementPrePre, "-"),?ElementPrePre) as ?ElementPre )
    bind ( if ( contains(?ElementPre, "-"), strafter(?ElementPre, "-"),?ElementPre) as ?Element )
    bind ( if ( contains(?ObjectTotal, "_"), strbefore(?ObjectTotal, "_"),strbefore(?ObjectTotal, "-")) as ?Object )
    bind ( if ( contains(?ObjectTotal, "_"), strafter(?ObjectTotal, "_"),strafter(?ObjectTotal, "-")) as ?SubObject01Total )
    bind ( if ( contains(?SubObject01Total, "_"), strbefore(?SubObject01Total, "_"),strbefore(?SubObject01Total, "-")) as ?SubObject01 )
    bind ( if ( contains(?SubObject01Total, "_"), strafter(?SubObject01Total, "_"),strafter(?SubObject01Total, "-")) as ?SubObject02Total )
    bind ( if ( contains(?SubObject02Total, "_"), strbefore(?SubObject02Total, "_"),strbefore(?SubObject02Total, "-")) as ?SubObject02 )
    bind ( if ( contains(?SubObject02Total, "_"), strafter(?SubObject02Total, "_"),strafter(?SubObject02Total, "-")) as ?SubObject03Total )
    bind ( if ( contains(?SubObject03Total, "_"), strbefore(?SubObject03Total, "_"),strbefore(?SubObject03Total, "-")) as ?SubObject03 )
    bind ( if ( contains(?SubObject03Total, "_"), strafter(?SubObject03Total, "_"),strafter(?SubObject03Total, "-")) as ?SubObject04Total )
    bind ( if ( contains(?SubObject04Total, "_"), strbefore(?SubObject04Total, "_"),strbefore(?SubObject04Total, "-")) as ?SubObject04 )
    bind ( if ( contains(?SubObject04Total, "_"), strafter(?SubObject04Total, "_"),strafter(?SubObject04Total, "-")) as ?SubObject05Total )
    bind ( if ( contains(?SubObject05Total, "_"), strbefore(?SubObject05Total, "_"),strbefore(?SubObject05Total, "-")) as ?SubObject05 )
    bind ( if ( contains(?SubObject05Total, "_"), strafter(?SubObject05Total, "_"),strafter(?SubObject05Total, "-")) as ?BewerkingTotal )
    bind ( if ( contains(?BewerkingTotal, "_"), strbefore(?BewerkingTotal, "_"),strbefore(?BewerkingTotal, "-")) as ?Bewerking )
}
</pre>


## Tabel sbibliotheken 
De [query voor sbibliotheken](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20sbibliotheken.rq) resulteert in een tabel zoals deze, rijen en kolommen omgekeerd voor betere weergave in dit document: 


| Abib | bibliotheek | Definitie | ID |
|------|-------------|-----------|----|
| [http://www.example.org/nlcs/def/e9c91921-3dc7-4c10-aba8-2d7e63c275e1](http://www.example.org/nlcs/def/e9c91921-3dc7-4c10-aba8-2d7e63c275e1) | SAL |  |  |
| [http://www.example.org/nlcs/def/de8fd506-a047-4789-a778-2c7c75035294](http://www.example.org/nlcs/def/de8fd506-a047-4789-a778-2c7c75035294) | SAM |  |  |
| [http://www.example.org/nlcs/def/ba6dd0d1-d3b2-4462-9828-3b21da7634c1](http://www.example.org/nlcs/def/ba6dd0d1-d3b2-4462-9828-3b21da7634c1) | SBC |  |  |



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all sbibliotheek info with URI, name, definition, and ID

SELECT DISTINCT ?Abib ?bibliotheek ?Definitie ?ID
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "Symbool" .
  ?Abib rdfs:subClassOf ?NLCSObjectTopConcept .
  ?Abib skos:prefLabel ?bibliotheek .
  OPTIONAL { ?Abib skos:definition ?Definitie . }
  OPTIONAL { ?Abib dcterms:identifier ?ID . }  # Assuming dcterms:identifier is used for the ID
}
ORDER BY ?bibliotheek
</pre>



## Tabel status 
De [query voor status](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20status.rq) resulteert in een tabel zoals deze:

| StatusURI | Status | Omschrijving |
|-----------|--------|---------------|
| http://www.example.org/nlcs/def/0bafa9e2-958b-47b2-aed6-6fd7d2cbcec4 | B | bestaand werk |
| http://www.example.org/nlcs/def/4ebfb892-cb19-40df-a3d9-91b1d21248c7 | N | nieuw werk |
| http://www.example.org/nlcs/def/d53156ad-de5a-4436-9720-c2bf82d09a12 | R | revisie |



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?StatusURI ?Status ?Omschrijving 
WHERE { 
    ?statusTopConcept skos:prefLabel "Statussen" .
    ?StatusURI a ?statusTopConcept ;
        skos:prefLabel ?Status ;
        skos:definition ?Omschrijving .
} 
ORDER BY ?Status
</pre>

<div class="issue" data-number="291"></div>



## Tabel symbolen
De [query voor symbolen](https://github.com/nl-digigo/NLCS/blob/main/code/Query%20symbolen.rq) resulteert in een tabel zoals deze:

| | Symbool 1 | Symbool 2 | Symbool 3 |
|---|---|---|---|
| Symbool | http://www.example.org/nlcs/def/628f9329-ec1f-47d4-9aeb-60b4c1895695 | http://www.example.org/nlcs/def/b7d51e67-1bb0-4244-bffb-88a7e5934ebc | http://www.example.org/nlcs/def/e3329046-59d7-425b-9f12-9a8684c4930e |
| FASE | B | B | B |
| sBIB | SAL | SGR | SGR |
| SYMBOOL | B-SAL-VERWIJZING_MATERIAALNUMMER-SO | B-SGR-BOOM_1E GROOTTE | B-SGR-BOOM_2E GROOTTE |
| SYMBOOL_alt | SAL:B-SAL-VERWIJZING_MATERIAALNUMMER | SGR:B-SGR-BOOM_1E GROOTT | SGR:B-SGR-BOOM_2E GROOTT |
| ID | 3004 | 3031 | 3032 |
| opt | SO |  |  |
| STATUS |  |  |  |
| VRKL_KORT |  |  |  |
| VRKL_LANG |  |  |  |



De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Symbool information including URIs and stores it in the familiar Excel/CSV format

SELECT DISTINCT ?Symbool ?FASE ?sBIB ?SYMBOOL ?SYMBOOL_alt ?ID ?opt ?STATUS ?VRKL_KORT ?VRKL_LANG
WHERE {
  ?SymboolTopConcept a owl:Class .
  ?SymboolTopConcept skos:prefLabel "Symbool" .
  ?Sbib rdfs:subClassOf ?SymboolTopConcept .
  ?Sbib skos:prefLabel ?sBIB .
  ?Symbool rdfs:subClassOf+ ?Sbib .
  ?Symbool skos:prefLabel ?SYMBOOL .
  OPTIONAL { ?Symbool skos:altLabel ?SYMBOOL_alt . }
  OPTIONAL {
    ?Symbool rdfs:subClassOf ?bnodeStatus .
    ?bnodeStatus owl:onProperty ?StatusProperty .
    ?StatusProperty skos:prefLabel "Status" .
    OPTIONAL { ?StatusProperty owl:hasValue/skos:prefLabel ?StatusValue . }
    BIND( COALESCE(?StatusValue, "*") AS ?STATUS) .
  }
  FILTER (CONTAINS(STR(?SYMBOOL), STR(CONCAT(STR(?sBIB), "-")))) 
  OPTIONAL {
    BIND (STRBEFORE(STR(?SYMBOOL), STR(CONCAT(CONCAT("-", STR(?sBIB)), "-" ))) AS ?FASE2) . 
    BIND (IF(CONTAINS(?FASE2, "-"), STRBEFORE(?FASE2, "-"), ?FASE2) AS ?FASE) 
  }
  OPTIONAL {
    BIND (STRAFTER(?SYMBOOL, CONCAT(STR(?sBIB), "-")) AS ?opt2) 
    BIND (IF(CONTAINS(?opt2, "-"), STRAFTER(?opt2, "-"), ?opt2) AS ?opt3)
    BIND (IF(CONTAINS(?opt3, "-"), STRAFTER(?opt3, "-"), ?opt3) AS ?opt4)
    BIND (IF(STRLEN(?opt4) > 4, "", ?opt4) AS ?opt)
  } 
  OPTIONAL {
    ?Symbool ?IDProperty ?ID .
    ?IDProperty skos:prefLabel "ID" .
  }
  OPTIONAL {
    ?Symbool ?VRKL_KORTProperty ?VRKL_KORT .
    ?VRKL_KORTProperty skos:prefLabel "VRKL_KORT" .
  } 
  OPTIONAL {
    ?Symbool ?VRKL_LANGProperty ?VRKL_LANG .
    ?VRKL_LANGProperty skos:prefLabel "VRKL_LANG" .
  }
}
ORDER BY ?SYMBOOL
</pre>

<div class="issue" data-number="283"></div>

<div class="issue" data-number="284"></div>


## Wijzigingen tussen versies

<div class="issue" data-number="149"></div>

De [query voor ...]() resulteert in een objectentabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
</pre>



## Tabel bgt mapping
De [query voor ...]() resulteert in een tabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
</pre>

## Tabel GWSW mapping

De [query voor ...]() resulteert in een tabel zoals deze:

De query ziet er als volgt uit: 

<pre data-codelanguage="sparql">
</pre>




