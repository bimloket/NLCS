PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves all Lijntype information and stores it in the familiar Excel/CSV format

SELECT DISTINCT ?ID ?HOOFDGROEP ?LIJNTYPE ?DEFINITIE ?opt ?FASE ?STATUS
WHERE {
  ?LijntypeTopConcept a owl:Class .
  ?LijntypeTopConcept skos:prefLabel "Lijntype" .
  ?Hoofdgroep rdfs:subClassOf ?LijntypeTopConcept .
  ?Hoofdgroep skos:prefLabel ?HoofdgroepName .
  ?Lijntype rdfs:subClassOf* ?Hoofdgroep .
  ?Lijntype skos:prefLabel ?LIJNTYPE .
  BIND ( IF(?Lijntype = ?Hoofdgroep, IF(?LIJNTYPE IN ("V-CONTINUOUS-SO", "CONTINUOUS"), ?Lijntype, ?undefined), ?Lijntype) AS ?NotHoofdgroep) .
  BIND ( IF(?Lijntype = ?Hoofdgroep, IF(?LIJNTYPE IN ("V-CONTINUOUS-SO", "CONTINUOUS"), ?undefined, ?HoofdgroepName), ?HoofdgroepName) AS ?HOOFDGROEP) .
  FILTER (?Lijntype = ?NotHoofdgroep) .
  OPTIONAL {
    BIND ( STRAFTER(?LIJNTYPE,"-") as ?opt2)
    BIND ( IF ( CONTAINS(?opt2,"-"),STRAFTER(?opt2,"-"),?opt2) as ?opt3 )
    BIND ( IF ( CONTAINS(?opt3,"-"),STRAFTER(?opt3,"-"),?opt3) as ?opt4 )
    BIND ( IF (STRLEN(?opt4)>4,"",?opt4) as ?opt ) 
  } OPTIONAL {
    ?Lijntype ?IDProperty ?ID .
    ?IDProperty skos:prefLabel "ID" .
  } OPTIONAL {
    ?Lijntype skos:definition ?DEFINITIE .
  } OPTIONAL {
    BIND (STRBEFORE(STR(?LIJNTYPE), STR(CONCAT(CONCAT("-", STR(?HOOFDGROEP)), "-" ))) AS ?FASE) . 
  } OPTIONAL {
    ?Lijntype rdfs:subClassOf ?bnodeStatus .
  	?bnodeStatus owl:onProperty ?StatusProperty .
  	?StatusProperty skos:prefLabel "Status" .
	OPTIONAL {
      ?StatusProperty owl:hasValue/skos:prefLabel ?StatusValue .
    }
  }
  BIND( COALESCE(?StatusValue, "*") AS ?STATUS) .
}
orderby ?ID
